in main;
use std::{io, fs::_, time::_, rand};

mod balder;

use balder::{math::_, scene::node::_, core::_, interface};
use balder::core::memory::_;
use balder::interface::{_, widget::_};

use balder::core::{shader::_,
                   application::_, driver::_,
                   application::drawer::_};

use balder::utils::_;

use vulkan::_;
use sdl2::_;

class MyActivity over Activity {

    let dmut _manager : (&WidgetManager)? = none;
    let dmut _label : (&Label)? = none;
    let mut _lastFrame : u32 = 0u32;

    pub self (dmut app : &Application)
        with super (alias app)
    {
        let dmut man = copy WidgetManager (alias app);

        let dmut floating = copy FloatingLayout (alias man, "float");
        let dmut input = copy InputText (alias man,
                                         "input",
                                         "res:/fonts/generic.otf", 18,
                                         xalign-> XAlign::LEFT);

        let dmut fpsLabel = copy Label (alias man,
                                        "fps",
                                        "res:/fonts/generic.otf", 18,
                                        xalign-> XAlign::LEFT,
                                        yalign-> YAlign::TOP,
                                        color-> ColorPalette.get (DefaultColorPalettes::PURPLE, 0.f));

        floating:.insertChild (alias fpsLabel,
                               x-> WidgetSize (pix-> 10),
                               y-> WidgetSize (pix-> 10),
                               withEvents-> false);

        floating:.insertChild (alias input,
                               x-> WidgetSize (pix-> 100),
                               y-> WidgetSize (pix-> 100),
                               width-> WidgetSize (pix-> 200),
                               height-> WidgetSize (pix-> 30));


        let dmut box = copy Box (alias man, "box");

        let dmut scrollLinear = copy LinearLayout (alias man, "sc");
        scrollLinear:.insertChild (WidgetSize (pix-> 1000), alias floating);
        let dmut scrollbar = copy ScrollLayout (alias man, "scroll", alias scrollLinear);

        let dmut linear = copy LinearLayout (alias man, "lin");
        linear:.insertChild (WidgetSize (relative-> 0.5f), alias scrollbar);
        linear:.insertChild (WidgetSize (relative-> 0.5f), alias box);

        man:.setContent (alias linear);



        self._label = (alias fpsLabel)?;
        self._manager = (alias man)?;
    }

    pub over onStart (mut self) {
        self:.input:.connectQuit (&self:.onQuit);
        self:.input:.connect (KeyInfo (SDLK_r, KeyEvent::ALL), &self:.redraw);
    }

    pub fn redraw (mut self, _ : KeyInfo) {
        self:.window:.redraw ();
    }

    pub over onUpdate (mut self, _delta_ : Duration) {
        if let Ok (dmut l) = alias self._label {
            let fps = self:.window.getNbFramesLastSecond ();
            if fps != self._lastFrame {
                l:.setText ("FPS : " ~ std::conv::to![c8] (fps))?;
                self._lastFrame = fps;
            }
        }

        // println (_delta_);
    }

    pub fn onQuit (mut self) {
        log::info #("Main", "Closing app");
        self._app:.quit ();
    }

    impl Disposable {
        pub over dispose (mut self) {
            if let Ok (dmut w) = alias self._manager {
                w:.dispose ();
                self._manager = none;
            }
        }
    }

}


fn main () {
    {
        let cfg = copy BalderConfig ("res:/config/app.toml");
        let dmut app = copy Application!{&MyActivity} (cfg);
        app:.run ();
        app:.dispose ();
    } catch {
        bd => {
            eprintln (bd);
        }
    }
} 
