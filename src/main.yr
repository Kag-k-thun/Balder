in main;
use std::{io, fs::_, time::_, rand};

mod balder;

use balder::{math::_, scene::node::_, core::_, interface};
use balder::interface::{_, widget::_};

use balder::core::{shader::_,
                   application::_, driver::_,
                   application::drawer::_};

use balder::utils::_;

use vulkan::_;
use sdl2::_;

class MyActivity over Activity {

    let dmut _manager : (&WidgetManager)? = none;

    pub self (dmut app : &Application)
        with super (alias app)
    {
        let dmut man = copy WidgetManager (alias app);
        let dmut float = copy FloatingLayout (alias man, "root");
        let dmut box1 = copy Box (alias man, "box1", color-> makeVec4 (1.f, 0.f, 0.f, 0.9f));
        let dmut box2 = copy Box (alias man, "box2", color-> makeVec4 (0.f, 1.f, 0.f, 1.f));

        float:.insertChild (alias box2);
        float:.insertChild (alias box1,
                            x-> WidgetSize (minPix-> 20), y-> WidgetSize (minPix-> 20),
                            width-> WidgetSize (relative-> 0.2f), height-> WidgetSize (relative-> 0.2f));

        man:.setContent (alias float);

        self._manager = (alias man)?;
    }

    pub over onStart (mut self) {
        self:.input:.connectQuit (&self:.onQuit);
        self:.input:.connect (KeyInfo (SDLK_r, KeyEvent::ALL), &self:.redraw);
    }

    pub fn redraw (mut self, _ : KeyInfo) {
        self:.window:.redraw ();
    }

    pub over onUpdate (mut self, _delta_ : Duration) {
        println (_delta_);
    }

    pub fn onQuit (mut self) {
        log::info ("Closing app");
        self._app:.quit ();
    }

    impl Disposable {
        pub over dispose (mut self) {
            if let Ok (dmut w) = alias self._manager {
                w:.dispose ();
                self._manager = none;
            }
        }
    }

}


fn main () {
    {
        let cfg = copy BalderConfig ("res:/config/app.toml");
        let dmut app = copy Application!{&MyActivity} (cfg);
        app:.run ();
        app:.dispose ();
    } catch {
        bd => {
            eprintln (bd);
        }
    }
} 
