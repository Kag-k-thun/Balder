in palette;

use balder::math::_;
use std::algorithm::comparison;

pub lazy dmut ColorPalette = alias constructDefaultPalettes ();

pub fn colorFromHex (u : u32)-> vec4 {
    let r = (u >> 16u32) & 0xFFu32;
    let g = (u >> 8u32) & 0xFFu32;
    let b = (u) & 0xFFu32;
    return makeVec4 (cast!f32 (r) / 255.0f,
                     cast!f32 (g) / 255.0f,
                     cast!f32 (b) / 255.0f,
                     1.f);
}

pub enum
| GREY   = "grey"
| PURPLE = "purple"
| GREEN  = "green"
 -> DefaultColorPalettes;

/**
 *
 * */
class ColorPaletteFactory {

    // The list of registered palettes
    let mut _palettes : [[c8] => mut [vec4]] = copy [];

    pub self () {}

    pub fn insert (mut self, name : [c8], palette : [vec4]) {
        self._palettes [name] = palette;
    }

    pub fn get (self, name : [c8], grad : f32)-> vec4 {
        if let Ok (pal) = self._palettes [name] {
            let index = clamp (grad, 0.f, 1.f) * cast!f32 (cast!u32 (pal.len));

            let indexLow = min (cast!u32 (pal.len - 1), cast!u32 (index));
            let indexHigh = min (cast!u32 (pal.len - 1), indexLow + 1);

            let lowColor = pal [indexLow];
            let highColor = pal [indexHigh];


            let dist = cast!f32 (indexHigh) - index;
            let invDist = 1.f - dist;
            let result = lowColor * dist + highColor * invDist;

            return result;
        }

        return vec4 ()
    }

}

fn constructDefaultPalettes ()-> dmut &ColorPaletteFactory {
    let dmut palette = copy ColorPaletteFactory ();

    palette:.insert ("grey", copy [colorFromHex (0x212529),
                                   colorFromHex (0x343A40),
                                   colorFromHex (0x495057),
                                   colorFromHex (0x6C757D),
                                   colorFromHex (0xADB5BD),
                                   colorFromHex (0xCED4DA),
                                   colorFromHex (0xDEE2E6),
                                   colorFromHex (0xE9ECEF),
                                   colorFromHex (0xF8F9FA)]);

    palette:.insert ("purple", copy [colorFromHex (0x6247AA),
                                     colorFromHex (0x7251B5),
                                     colorFromHex (0x815AC0),
                                     colorFromHex (0x9163CB),
                                     colorFromHex (0xA06CD5),
                                     colorFromHex (0xB185DB),
                                     colorFromHex (0xC19EE0),
                                     colorFromHex (0xD2B7E5),
                                     colorFromHex (0xDAC3E8),
                                     colorFromHex (0xDEC9E9)]);

    palette:.insert ("green", copy [colorFromHex (0x004B23),
                                    colorFromHex (0x006400),
                                    colorFromHex (0x38B000),
                                    colorFromHex (0x70E000),
                                    colorFromHex (0x9EF01A),
                                    colorFromHex (0xCCFF33)]);

    alias palette
}
