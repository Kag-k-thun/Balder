in diffuse;

use balder::core::_;
use balder::core::shader::_;
use balder::core::shader::buffers::_;
use balder::core::application::_;

use balder::core::config::{_, shader::_};

use balder::math::_;

pub class DiffuseTextureMaterial over Material {

    let dmut _window : (&Window)? = none;    
    let dmut _texture : (&RefCount!{&Texture})? = none;
    let dmut _buffer : (&UniformBufferObject)? = none;
    
    let _texturePath : [c8];
    
    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ======================================          CTOR          ======================================
     * ====================================================================================================
     * ====================================================================================================
     */

    pub self (texturePath : [c8], config : MaterialConfig)
        with super (loadRasterShaderConfig (DefaultShaders::TEXTURE_3D), config)                            
        , _texturePath = texturePath        
        throws BalderError
    {}

    __dtor (mut self) {
        self:.dispose ()
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * =====================================          USAGE          ======================================
     * ====================================================================================================
     * ====================================================================================================
     */

    /**
     * Attach the material to a shader
     * */
    pub over attach (mut self, dmut window : &Window, dmut set : &DescriptorSet, matID : u32)
        throws BalderError
    {
        self:.dispose ();
        
        let dmut tex = window:.loadResource!{&Texture2D} (self._texturePath);        
        let dmut buffer = copy UniformBufferObject (set:.getShader ():.getDevice (), 4);
        buffer:.setData (0, matID, commit-> true);

        set:.setUniform (UniformNames::MATERIAL, buffer);
        set:.setTexture (TextureKind::ALBEDO, tex:.get ());

        self._buffer = (alias buffer)?;
        self._texture = (alias tex)?;
        self._window = (alias window)?;        
        self._matID = matID;
    }

    pub over detach (mut self) {
        if let Ok (dmut tex) = alias self._texture {
            tex:.dispose ();
            self._texture = none;
        }


        if let Ok (dmut win) = alias self._window {
            if let Ok (dmut c) = alias self._buffer {
                win:.insertBin (alias c);
                self._buffer = none;
            }
            self._window = none;
        }
    }
    
    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ====================================          DISPOSE          =====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    impl Disposable {
        pub over dispose (mut self) {
            self:.detach ();
        }
    }

}
