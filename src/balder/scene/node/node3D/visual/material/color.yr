in color;

use balder::core::_;
use balder::core::shader::_;
use balder::core::shader::buffers::_;
use balder::core::application::_;

use balder::core::config::_;

use balder::math::_;

pub class ColorMaterial over Material {

    let dmut _window : (&Window)? = none;
    let dmut _buffer : (&UniformBufferObject)? = none;

    let mut _value : vec4 = makeVec4 (1.f, 1.f, 1.f, 1.f);

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ======================================          CTOR          ======================================
     * ====================================================================================================
     * ====================================================================================================
     */

    pub self (color : vec4)
        with super (copy ShaderConfig (DefaultShaders::COLOR_3D),
                    MaterialConfig (roughness-> 0.2f, metallic-> 1.f))
        
        , _value = color
        throws BalderError
    {}

    __dtor (mut self) {
        self:.dispose ()
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * =====================================          USAGE          ======================================
     * ====================================================================================================
     * ====================================================================================================
     */

    /**
     * Attach the material to a shader
     * */
    pub over attach (mut self, dmut window : &Window, dmut set : &DescriptorSet, matID : u32)
        throws BalderError
    {
        self:.dispose ();
        
        let dmut buffer = copy UniformBufferObject (set:.getShader ():.getDevice (), 20);
        buffer:.setData (0, self._value);
        buffer:.setData (16, matID, commit-> true);
                
        set:.setUniform (UniformNames::MATERIAL, buffer);

        self._window = (alias window)?;
        self._buffer = (alias buffer)?;
        self._matID = matID;
    }

    pub over detach (mut self) {
        if let Ok (dmut win) = alias self._window {
            if let Ok (dmut c) = alias self._buffer {
                win:.insertBin (alias c);
                self._buffer = none;
            }

            self._window = none;
        }
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ====================================          GET/SET          =====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    pub fn setColor (mut self, color : vec4) {
        if self._value != color {
            self._value = color;
            if let Ok (dmut ubo) = alias self._buffer {
                ubo:.setData (0, self._value, commit-> true);
            }
        }
    }

    @field
    pub fn color (self)-> vec4 {
        self._value
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ====================================          DISPOSE          =====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    impl Disposable {
        pub over dispose (mut self) {
            self:.detach ();
        }
    }

}
