in shape;

use balder::math::_;
use balder::core::{_,
                   shader::_,
                   shader::buffers::_,
                   application::_,
                   driver::_,
                   config::_};

use balder::utils::_;
use balder::interface::drawer;

pub mod ::color;
pub mod ::texture;


/**
 * Ancestor class of shapes that are drawn in 2D by the ShapeDrawer
 * */
@abstract
pub class Shape {

    pub let uid : usize;

    let dmut _context : &ShapeDrawer;

    let _vbo : &VertexBufferObject;
    let _ibo : &IndexBufferObject;

    let dmut _ubo : &UniformBufferObject;
    let dmut _descSet : &DescriptorSet;

    let mut _hidden : bool = false;
    let mut _outOfBound : bool = false;
    let mut _needRedraw : bool = false;
    let mut _changed : bool = false;

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ===================================          CTOR/DTOR          ====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    pub self (uid : usize,
              dmut drawer : &ShapeDrawer,
              vbo : &VertexBufferObject,
              ibo : &IndexBufferObject,
              dmut ubo : &UniformBufferObject,
              dmut descSet : &DescriptorSet)

        with uid = uid
        , _context = alias drawer
        , _vbo = vbo
        , _ibo = ibo
        , _ubo = alias ubo
        , _descSet = alias descSet

        throws BalderError
    {
        self._descSet:.setUniform (UniformNames::WORLD, self._ubo);
    }

    __dtor (mut self) {
        self:.dispose ();
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ====================================          SETTERS          =====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    /**
     * Change the dimension of the window in which the shape is drawn
     * */
    pub fn setWindowDimension (mut self, dim : vec2) {
        self._ubo:.setData (0, dim, commit-> false);
        self:.setScissors (makeVec2 (0.f, 0.f), dim);

        let pos = self._ubo.getData!{vec2} (8);
        let size = self._ubo.getData!{vec2} (16);

        let outOfBound = (pos.x + size.x < -(dim.x / 2.f) || pos.x > dim.x * 1.5f  || pos.y + size.y < -(dim.y / 2.f) || pos.y > dim.y * 1.5f);
        if self._outOfBound != outOfBound {
            self._outOfBound = outOfBound;
            self._needRedraw = true;
        }

        self._changed = true;
    }

    /**
     * Change the position of the shape in the window (in pixels, (0,0) being top left corner)
     * */
    pub fn setPosition (mut self, pos : vec2) {
        self._ubo:.setData (8, pos, commit-> false);

        let dim = self._ubo.getData!{vec2} (0);
        let size = self._ubo.getData!{vec2} (16);

        let outOfBound = (pos.x + size.x < -(dim.x / 2.f) || pos.x > dim.x * 1.5f  || pos.y + size.y < -(dim.y / 2.f) || pos.y > dim.y * 1.5f);
        if self._outOfBound != outOfBound {
            self._outOfBound = outOfBound;
            self._needRedraw = true;
        }

        self._changed = true;
    }

    /**
     * Change the size of the shape in the window (in pixels)
     */
    pub fn setSize (mut self, size : vec2) {
        self._ubo:.setData (16, size, commit-> false);

        let dim = self._ubo.getData!{vec2} (0);
        let pos = self._ubo.getData!{vec2} (8);

        let outOfBound = (pos.x + size.x < -(dim.x / 2.f) || pos.x > dim.x * 1.5f  || pos.y + size.y < -(dim.y / 2.f) || pos.y > dim.y * 1.5f);
        if self._outOfBound != outOfBound {
            self._outOfBound = outOfBound;
            self._needRedraw = true;
        }

        self._changed = true;
    }

    /**
     * Change the roundeness of the corner of the shape
     */
    pub fn setRadius (mut self, v : u32) {
        self._ubo:.setData (24, v, commit-> false);
        self._changed = true;
    }

    /**
     * Change the roundeness of the corner of the shape
     */
    pub fn setLevel (mut self, v : u32) {
        self._ubo:.setData (28, v, commit-> false);
        self._changed = true;
    }

    /**
     * Change the color of the shape
     */
    pub fn setColor (mut self, v : vec4) {
        self._ubo:.setData (32, v, commit-> false);
        self._changed = true;
    }

    /**
     * Change the rotation of the shape
     */
    pub fn setRotation (mut self, v : f32) {
        let m = math::utils::rotationZ2 (radian (v % 360.f));
        self._ubo:.setData (48, makeVec4 (m.a1, m.b1, m.a2, m.b2), commit-> false);
        self._changed = true;
    }

    /**
     * Change the scissors of the shape
     */
    pub fn setScissors (mut self, pos : vec2, size : vec2) {
        self._ubo:.setData (64, makeVec4 (pos.x, pos.y, pos.x + size.x, pos.y + size.y), commit-> false);
        self._changed = true;
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ===================================          HIDE/SHOW          ====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    /**
     * Hide the shape in the next drawing
     * */
    @final
    pub fn hide (mut self) {
        if !self._hidden {
            self._hidden = true;
            self._needRedraw = true;
        }
    }

    /**
     * Show the shape in the next drawing
     * */
    @final
    pub fn show (mut self) {
        if self._hidden {
            self._hidden = false;
            self._needRedraw = true;
        }
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * =====================================          COMMIT          =====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    /**
     * Commit the modifications of the shape to be rendered in the next frame
     * */
    pub fn commit (mut self)-> bool {
        let mut toReturn = false;
        if self._changed && !self.isHidden () {
            self._ubo:.forceCommit ();
            self._changed = false;

            toReturn = true;
        }

        if self._needRedraw {
            self._context:.redraw ();
            self._needRedraw = false;
        }

        toReturn
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ====================================          GETTERS          =====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    /**
     * @returns: true iif the shape is hidden (and therefore must not be drawn)
     * */
    @final
    pub fn isHidden (self)-> bool {
        self._hidden || self._outOfBound
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * =====================================          USAGE          ======================================
     * ====================================================================================================
     * ====================================================================================================
     */

    /**
     * Register the drawing in the current draw iteration
     * */
    pub fn register (mut self, dmut cmd : &IndirectCommandAllocator) {
        cmd:.registerIndexedObject (self._ibo.getNbPoints ());
    }

    /**
     * Draw the shape
     * @assume: already registered and at correct index
     * */
    pub fn draw (self, dmut _ : &Shader, dmut cmd : &IndirectCommandAllocator) {
        self._descSet.select ();
        self._vbo.select ();
        self._ibo.select ();

        cmd:.drawNextIndexed ();
    }

    /**
     * Clean phase of a draw pass
     * */
    pub fn clean (mut self) {}

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ===================================          DISPOSING          ====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    impl Disposable {
        pub over dispose (mut self) {
            self._context:.getWindow ():.insertBin (alias self._descSet);
            self._context:.getWindow ():.insertBin (alias self._ubo);
        }
    }

}
