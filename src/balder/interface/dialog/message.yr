in message;

use balder::{math::_, scene::node::_, core::_, interface};
use balder::core::application::_;
use balder::interface::widget::text::label;

use balder::utils::log;

use ::sdl2::_;

pub enum : u8
| UNDEFINED = 0
| YES       = 1
| NO        = 2
| CANCEL    = 3
 -> MessageDialogStatus; 


pub class MessageActivity over Activity {

    let mut _message : [c8] = "";
    let dmut _dialog : (&MessageDialog)? = none;

    pub self (uid : usize, dmut app : &Application, dmut window : &Window)
        with super (uid, alias app, alias window)
    {}

    pub fn setContext (mut self, message : [c8], dmut d : &MessageDialog) {
        self._dialog = (alias d)?;
        self._message = message;
    }

    over onStart (mut self) {
        self.input:.quit ():.connect (self.box, &self:.onQuit);
        std::io::println (self.gui:.loadStyleFile ("res:/dialog/default.style")?);
        std::io::println (self.gui:.loadGUIFile ("res:/dialog/message.gui")?);

        if let Ok (dmut yes) = alias self.gui:.find ("F2/YES") {
            yes:.clicked ():.connect (self.box, &self:.onYesClicked);
        }

        if let Ok (dmut no) = alias self.gui:.find ("F2/NO") {
            no:.clicked ():.connect (self.box, &self:.onNoClicked);
        }

        if let Ok (dmut question : &Label) = self.gui:.find ("QUESTION") {
            question:.setText (self._message);
        }
    }

    fn onYesClicked (mut self, b : u8) {
        if b == SDL_BUTTON_LEFT {            
            if let Ok (dmut d) = alias self._dialog {
                d:.answer (MessageDialogStatus::YES);                
            }
            self.app:.close (alias self);
        }
    }

    fn onNoClicked (mut self, b : u8) {
        if b == SDL_BUTTON_LEFT {            
            if let Ok (dmut d) = alias self._dialog {
                d:.answer (MessageDialogStatus::NO);                
            }
            self.app:.close (alias self);
        }
    }

    fn onQuit (mut self) {
        if let Ok (dmut d) = alias self._dialog {
            d:.answer (MessageDialogStatus::CANCEL);
        }
        self.app:.close (alias self);
    }

}


pub class MessageDialog over DialogWindow {

    let _message : [c8];
    let mut _status : MessageDialogStatus = MessageDialogStatus::UNDEFINED;

    pub self (dmut app : &Application, message : [c8])
        with super (alias app)
        , _message = message
    {}

    pub over execute (mut self) {
        {
            let cfg = copy BalderConfig ("res:/dialog/message.toml");

            let dmut act = self._app:.open!{&MessageActivity} (cfg);
            act:.setContext (self._message, alias self);

            self._sem:.wait ();
        } catch {
            err => {
                log::error #("MessageDialog", "Failed to open activity", err);
            }
        }
    }

    @field
    pub fn status (self)-> MessageDialogStatus {
        self._status
    }

    pub fn answer (mut self, status : MessageDialogStatus) {
        self._status = status;
        self._sem:.post ();
    }

}
