in label;

use balder::core::{shader::_,
                   application::{_, shape::_}};

use balder::core::_;
use balder::math::_;

use balder::interface::_;
use balder::utils::_;

use std::io;
use std::algorithm::comparison;


pub def __LABEL_DEFAULT_STYLE__ = "default.text.label";

pub class Label over Widget {

    // The text displayed by the label
    let mut _text : [c16];

    prot {  // Display configuration

        // The alignement of the text along the x axis
        let mut _xalign : XAlign;

        // The alignement of the text along the y axis
        let mut _yalign : YAlign;

        let mut _relTextSize : vec2 = vec2 ();

        let mut _relTextPos : vec2 = vec2 ();

    }

    prot {

        // The texture of the shape
        let dmut _texture : (&Texture)? = none;
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ======================================          CTOR          ======================================
     * ====================================================================================================
     * ====================================================================================================
     */

    pub self (dmut manager : &WidgetManager,
              name : [c8],
              xalign : XAlign = XAlign::CENTER,
              yalign : YAlign = YAlign::CENTER,
              style : [c8] = __LABEL_DEFAULT_STYLE__)

        with super (alias manager, name,
                    style-> style)

        , _text = ""
        , _xalign = xalign
        , _yalign = yalign
    {
        self:.registerShape (WidgetShapeKind::TEXT);
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ===================================          DRAW SIZES          ===================================
     * ====================================================================================================
     * ====================================================================================================
     */

    over onUpdateSize (mut self) {
        if let Ok (d : &Texture2D) = self._texture {
            let relSize = makeVec2 (cast!f32 (d.width), cast!f32 (d.height)) / self._absoluteSize;

            let mut x = if self._xalign == XAlign::LEFT || self._xalign == XAlign::NONE {
                0.f
            } else if self._xalign == XAlign::RIGHT {
                1.f - relSize.x
            } else { 0.5f - (relSize.x / 2.f) };

            let mut y = if self._yalign == YAlign::TOP {
                0.f
            } else if self._yalign == YAlign::BOTTOM {
                1.f - relSize.y
            } else {0.5f - (relSize.y / 2.f) };

            if self._xalign == XAlign::NONE {
                x = max (0.f, x);
            }

            y = max (0.f, y);

            self:.setShapesPosAndSize (WidgetShapeKind::TEXT,
                                       self._absolutePosition + (makeVec2 (x, y) * self._absoluteSize),
                                       relSize * self._absoluteSize);

            if let Ok ([dmut ws]) = alias self._shapes [WidgetShapeKind::TEXT] {
                if let Ok (dmut sh : &TextureShape) = alias ws.shape {
                    ws.commit = (self._relTextSize == relSize);
                    ws.hidden = false;
                    sh:.show ();
                }
            }

            self._relTextPos = makeVec2 (x, y);
            self._relTextSize = relSize;
        } else {
            self:.hide ();
            self._relTextSize = vec2 ();
            self._relTextPos = vec2 ();
        }
    }

    over onUpdateScissors (mut self) {
        if let Ok ([dmut sh]) = alias self._shapes [WidgetShapeKind::TEXT] {
            sh.commit = true;
        }
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ====================================          SETTERS          =====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    /**
     * Set the text of the label
     * */
    pub fn setText (mut self, text : [c8])
        throws BalderError
    {
        self:.setText (std::conv::to!{[c16]} (text));
    }

    /**
     * Set the text of the label
     * */
    pub fn setText (mut self, text : [c16])
        throws BalderError
    {
        let dmut old = alias self._texture;
        self._texture = alias self._manager:.getFontManager ():.render (text, self.getFont (), self.getFontSize ());
        self:.computeSize ();

        if let Ok ([dmut ws]) = alias self._shapes [WidgetShapeKind::TEXT] {
            if let Ok (dmut sh : &TextureShape) = alias ws.shape {
                if let Ok (d : &Texture2D) = self._texture {
                    sh:.setSize (makeVec2 (cast!f32 (d.width), cast!f32 (d.height)));
                    sh:.setTexture (self._texture);
                    sh:.show ();
                    ws.hidden = false;
                } else {
                    sh:.hide ();
                    ws.hidden = true;
                }

                sh:.commit (true);
            }
        }

        if let Ok (dmut o) = alias old {
            let dmut bin = self._manager:.getWindow ():.getGarbageBin ();
            bin:.insert (alias o);
        }
    }

    /**
     * Change the vertical alignement of the label
     * */
    pub fn setXAlign (mut self, xalign : XAlign) {
        self._xalign = xalign;
    }

    /**
     * Change the horizontal alignement of the label
     * */
    pub fn setYAlign (mut self, yalign : YAlign) {
        self._yalign = yalign;
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ====================================          GETTERS          =====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    /**
     * @returns: the font used by the label
     * */
    pub fn getFont (self)-> [c8] {
        self.getStyle ().font._1
    }

    /**
     * @returns: the size of the font
     * */
    pub fn getFontSize (self)-> u32 {
        self.getStyle ().fontSize._1
    }

    /**
     * @returns: the size of the displayed text in pixels
     * */
    pub fn getTextSize (self)-> vec2 {
        self._relTextSize * self._absoluteSize
    }

    /**
     * @returns: the position of the displayed text in pixels
     * */
    pub fn getTextPos (self)-> vec2 {
        self._absolutePosition + (self._relTextPos * self._absoluteSize)
    }

    /**
     * @returns: the horizontal alignement of the label
     * */
    pub fn getXAlign (self)-> XAlign {
        self._xalign
    }

    /**
     * @returns: the vertical alignement of the label
     * */
    pub fn getYAlign (self)-> YAlign {
        self._yalign
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ===================================          DISPOSING          ====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    impl Disposable {
        pub over dispose (mut self) {
            self:.disposeShapes ();

            let dmut bin = self._manager:.getWindow ():.getGarbageBin ();
            if let Ok (dmut t) = alias self._texture {
                bin:.insert (alias t);
                self._texture = none;
            }
        }
    }

}
