in label;

use balder::core::{shader::_, application::_};
use balder::core::_;
use balder::math::_;

use balder::interface::_;
use balder::utils::_;

use std::io;
use std::algorithm::comparison;

pub class Label over Widget {

    // The text displayed by the label
    let mut _text : [c8];

    prot {  // Display configuration
        let _color : vec4;
        let _fontSize : u32;
        let _fontPath : [c8];

        // The size of the font in pt (applicable only if != -1.f)
        let _forcedSize : f32;

        // The alignement of the text along the x axis
        let _xalign : XAlign;

        // The alignement of the text along the y axis
        let _yalign : YAlign;

    }

    prot {

        // Shape drawn on screen
        let dmut _shape : (&Shape)? = none;

        // The texture of the shape
        let dmut _texture : (&Texture)? = none;
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ======================================          CTOR          ======================================
     * ====================================================================================================
     * ====================================================================================================
     */

    pub self (dmut manager : &WidgetManager, name : [c8],
              fontPath : [c8],
              fontSize : u32,
              color : vec4 = makeVec4 (1.f, 1.f, 1.f, 1.f),
              forcedSize : f32 = -1.f,
              xalign : XAlign = XAlign::CENTER,
              yalign : YAlign = YAlign::CENTER)

        with super (alias manager, name)
        , _color = color
        , _text = ""
        , _fontPath = fontPath
        , _fontSize = fontSize
        , _xalign = xalign
        , _yalign = yalign
        , _forcedSize = forcedSize
    {
        let dmut shDr = manager:.getApplication ():.getShapeDrawer ();

        {
            let dmut shape = shDr:.registerTextureQuad (vec2 (), self._color, vec2 (), texture-> self._texture);
            self._shape = (alias shape)?;
        } catch {
            err => {
                log::error #("Label", "Failed to add a shape : ", err);
            }
        }
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ===================================          DRAW SIZES          ===================================
     * ====================================================================================================
     * ====================================================================================================
     */

    pub over onUpdateSize (mut self, pos : vec2, size : vec2, depth : u32) {
        self._absoluteSize = size;
        self._absolutePosition = pos;
        self._depth = depth;

        if let Ok (d : &Texture2D) = self._texture {
            let texS = makeVec2 (d.width, d.height);

            let ratio = if self._forcedSize != -1.f {
                self._forcedSize / cast!f32 (self._fontSize)
            } else { 1.f };

            let relSize = makeVec2 (cast!f32 (texS.x), cast!f32 (texS.y)) * ratio / size;

            let mut x = if relSize.x > 1.f {
                0.97f - relSize.x
            } else if self._xalign == XAlign::LEFT {
                0.f
            } else if self._xalign == XAlign::RIGHT {
                1.f - relSize.x
            } else { 0.5f - (relSize.x / 2.f) };

            let mut y = if self._yalign == YAlign::TOP {
                0.f
            } else if self._yalign == YAlign::BOTTOM {
                1.f - relSize.y
            } else {0.5f - (relSize.y / 2.f) };

            x = max (0.f, x);
            y = max (0.f, y);

            if let Ok (dmut sh) = alias self._shape {
                sh:.setPosition (pos + makeVec2 (x, y) * size);
                sh:.setSize (relSize * size);
                sh:.setLevel (depth);
            }
        }
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ====================================          SETTERS          =====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    /**
     * Set the text of the label
     * */
    pub fn setText (mut self, text : [c8])
        throws BalderError
    {
        let dmut old = alias self._texture;
        self._texture = alias self._manager:.getFontManager ():.render (text, self._fontPath, self._fontSize);

        if let Ok (dmut sh) = alias self._shape {
            sh:.setTexture (self._texture)?;
        }

        if let Ok (dmut o) = alias old {
            let dmut bin = self._manager:.getWindow ():.getGarbageBin ();
            bin:.insert (alias o);
        }

        self:.onUpdateSize (self._absolutePosition, self._absoluteSize, self._depth);
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ===================================          DISPOSING          ====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    impl Disposable {
        pub over dispose (mut self) {
            if let Ok (dmut sh) = alias self._shape {
                let dmut shDr = self._manager:.getApplication ():.getShapeDrawer ();

                self._shape = none;
                shDr:.removeTextureShape (sh.uid);
            }

            let dmut bin = self._manager:.getWindow ():.getGarbageBin ();
            if let Ok (dmut t) = alias self._texture {
                bin:.insert (alias t);
                self._texture = none;
            }
        }
    }

}
