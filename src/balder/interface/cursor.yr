in cursor;

use ::sdl2::mouse;
use balder::core::dispose;

@final
pub class CursorManager {

    let dmut _cursors : [*SDL_Cursor ; cte { SDL_SystemCursor::__members__.len - 1 }] = [null ; cte { SDL_SystemCursor::__members__.len - 1 }];

    let _default : *SDL_Cursor = SDL_GetDefaultCursor ();

    let mut _currentKind : SDL_SystemCursor = SDL_SystemCursor::SDL_SYSTEM_CURSOR_ARROW;

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ===================================          CTOR/DTOR          ====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    pub self () {
        for i in 0 .. self._cursors.len {
            self._cursors [i] = SDL_CreateSystemCursor (SDL_SystemCursor::__members__ [i]);
        }
    }

    __dtor (mut self) {
        self:.dispose ();
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ====================================          GETTERS          =====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    /**
     * Get the current cursor icon of the mouse
     * */
    pub fn getCursor (self)-> SDL_SystemCursor {
        self._currentKind
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ====================================          SETTERS          =====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    /**
     * Change the cursor icon of the mouse
     * */
    pub fn setCursor (mut self, cursor : SDL_SystemCursor) {
        self._currentKind = cursor;
        if self._currentKind < self._cursors.len {
            let u : u32 = self._currentKind;
            SDL_SetCursor (self._cursors [u]);
        } else {
            SDL_SetCursor (self._default);
            self._currentKind = SDL_SystemCursor::SDL_SYSTEM_CURSOR_ARROW;
        }
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ===================================          DISPOSING          ====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    impl Disposable {
        pub over dispose (mut self) {
            SDL_SetCursor (self._default);
            for i in 0 .. self._cursors.len {
                if self._cursors [i] != null {
                    SDL_FreeCursor (alias self._cursors [i]);
                    self._cursors [i] = null;
                }
            }
        }
    }

}
