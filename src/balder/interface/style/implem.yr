in implem;

use std::algorithm::searching;

use balder::core::error;
use balder::interface::{_, widget::alignement};
use balder::math::_;
use balder::utils::palette;


/**
 * Style definition
 * */
pub class Style {

    pub { // colors
        let dmut backgroundColor : (bool, vec4) = (false, makeVec4 (0.f, 0.f, 0.f, 1.f));
        let dmut borderColor : (bool, vec4) = (false, makeVec4 (0.f, 0.f, 0.f, 1.f));
        let dmut textColor : (bool, vec4) = (false, makeVec4 (1.f, 1.f, 1.f, 1.f));
        let dmut selectionColor : (bool, vec4)  = (false, makeVec4 (1.f, 1.f, 1.f, 1.f));
        let dmut color : (bool, vec4)  = (false, makeVec4 (1.f, 1.f, 1.f, 1.f));
    }

    pub { // font
        let mut font : (mut bool, mut [c8]) = (false, "res:/fonts/arial.ttf");
        let dmut fontSize : (bool, u32)= (false, 14u32);
    }

    pub { // sizes
        let dmut borderSize : (bool, u32) = (false, 0u32);
        let dmut radius : (bool, u32) = (false, 0u32);
        let dmut paddingSize : [(bool, WidgetSize) ; 5] = [(false, WidgetSize (relative-> 1.f)) ; 5];
        let dmut marginSize : [(bool, WidgetSize) ; 5] = [(false, WidgetSize (relative-> 1.f)) ; 5];
        let dmut size : [(bool, WidgetSize) ; 3] = [(false, WidgetSize (relative-> 1.f)) ; 3];
        let dmut boundSize : [(bool, WidgetSize) ; 3] = [(false, WidgetSize (relative-> 1.f)) ; 3];
    }

    pub { // text alignment
        let dmut xalign : (bool, XAlign) = (false, XAlign::CENTER);
        let dmut yalign : (bool, YAlign) = (false, YAlign::CENTER);
    }

    pub { // background image
        let dmut imageColor : (bool, vec4) = (false, makeVec4 (1.f, 1.f, 1.f, 1.f));
        let mut backgroundImage : (mut bool, mut [c8]) = (false, "");
        let mut backgroundImageKeepRatio : (mut bool, mut bool) = (false, true);
        let mut backgroundImageFill : (mut bool, mut bool) = (false, true);
    }

    pub let mut subItems : [[c8] => mut &Style] = copy [];

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ======================================          CTOR          ======================================
     * ====================================================================================================
     * ====================================================================================================
     */

    pub self () {}

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ===================================          COMPARISON          ===================================
     * ====================================================================================================
     * ====================================================================================================
     */

    pub fn opEquals (self, other : &Style)-> bool {
        self.sameDimension (other) && self.sameColors (other)
    }

    /**
     * @returns: true if the dimension of the styles are equal
     * */
    pub fn sameDimension (self, other : &Style)-> bool {
        if self.font != other.font { return false; }
        if self.fontSize != other.fontSize { return false; }

        if self.borderSize != other.borderSize { return false; }
        if self.paddingSize != other.paddingSize { return false; }
        if self.marginSize != other.marginSize { return false; }
        if self.size != other.size { return false; }
        if self.backgroundImageFill != other.backgroundImageFill { return false; }
        if self.backgroundImageKeepRatio != other.backgroundImageKeepRatio { return false; }

        if self.xalign != other.xalign { return false; }
        if self.yalign != other.yalign { return false; }
        if self.radius != other.radius { return false; }

        for n, i in other.subItems {
            if let Ok (w) = self.subItems [n] {
                if !w.sameDimension (i) { return false; }
            } else {
                return false;
            }
        }

        for n, _ in self.subItems {
            if n !in other.subItems { return false; }
        }

        true
    }

    /**
     * @returns: true if the colors of the styles are equal
     * */
    pub fn sameColors (self, other : &Style)-> bool {
        if self.backgroundColor != other.backgroundColor { return false; }
        if self.borderColor != other.borderColor { return false; }
        if self.textColor != other.textColor { return false; }
        if self.selectionColor != other.selectionColor { return false; }
        if self.color != other.color { return false; }
        if self.backgroundImage != other.backgroundImage { return false; }
        if self.imageColor != other.imageColor { return false; }

        for n, i in other.subItems {
            if let Ok (w) = self.subItems [n] {
                if !w.sameColors (i) { return false; }
            } else {
                return false;
            }
        }

        for n, _ in self.subItems {
            if n !in other.subItems { return false; }
        }

        true
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ====================================          GETTERS          =====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    /**
     * @returns: the color to apply on a shape
     * */
    pub fn getColor (self, kind : WidgetShapeKind)-> vec4 {
        match kind {
            WidgetShapeKind::BACKGROUND => {
                if self.backgroundColor.0 {
                    return self.backgroundColor._1;
                }
            }
            WidgetShapeKind::IMAGE => {
                if self.imageColor._0 {
                    return self.imageColor._1;
                }
            }
            WidgetShapeKind::BORDER => {
                if self.borderColor.0 {
                    return self.borderColor._1
                }
            }
            WidgetShapeKind::TEXT => {
                if self.textColor.0 {
                    return self.textColor._1;
                }
            }
        }

        self.color._1
    }


    /**
     * @returns: the inner width of the widget
     * */
    pub fn getWidth (self)-> WidgetSize {
        if self.size [1].0 {
            return self.size [1].1;
        }

        return self.size [0].1;
    }

    /**
     * @returns: the height of the widget
     * */
    pub fn getHeight (self)-> WidgetSize {
        if self.size [2].0 {
            return self.size [2].1;
        }

        return self.size [0].1;
    }

    /**
     * @returns: true iif the width of the widget has been defined
     * */
    pub fn hasWidth (self)-> bool {
        self.size [1].0 || self.size [0].0
    }

    /**
     * @returns: true iif the height of the widget has been defined
     * */
    pub fn hasHeight (self)-> bool {
        self.size [2].0 || self.size [0].0
    }

    /**
     * @returns: the style associated to a component of the widget (i.e. scrollbar, label, ...)
     * */
    pub fn getSubStyle (self, name : [c8])-> &Style {
        if let Ok (s) = self.subItems [name] {
            return s;
        } else {
            return copy Style ();
        }
    }

    /**
     * @returns: the radius for round corners
     * */
    pub fn getRadius (self, kind : WidgetShapeKind)-> u32 {
        if !self.radius.0 || kind == WidgetShapeKind::SELECTION {
            return 0u32;
        }

        self.radius.1
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ====================================          MERGING          =====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    /**
     * Create a new style that is the merging of self and right where right has the priority
     * @params:
     *    - right: the style that modifies current style during merge
     *    - state: the state of the merging
     * */
    pub fn merge (mut self, right : &Style) {
        if right.backgroundColor.0 { self.backgroundColor = right.backgroundColor; }
        if right.textColor.0 { self.textColor = right.textColor; }
        if right.borderColor.0 { self.borderColor = right.borderColor; }
        if right.color.0 { self.color = right.color; }

        if right.imageColor.0 { self.imageColor = right.imageColor; }
        if right.backgroundImage.0 { self.backgroundImage = right.backgroundImage; }
        if right.backgroundImageKeepRatio.0 { self.backgroundImageKeepRatio = right.backgroundImageKeepRatio; }
        if right.backgroundImageFill.0 { self.backgroundImageFill = right.backgroundImageFill; }

        if right.font.0 { self.font = right.font; }
        if right.fontSize.0 { self.fontSize = right.fontSize; }

        if right.borderSize.0 { self.borderSize = right.borderSize; }
        if right.radius.0 { self.radius = right.radius; };

        if right.xalign.0 { self.xalign = right.xalign; }
        if right.yalign.0 { self.yalign = right.yalign; }

        for i in 0 .. 5 {
            if right.marginSize [i].0 { self.marginSize [i] = right.marginSize [i]; }
            if right.paddingSize [i].0 { self.paddingSize [i] = right.paddingSize [i]; };
        }

        for i in 0 .. 3 {
            if right.size [i].0 { self.size [i] = right.size [i]; }
        }

        for name, innerStyle in self.subItems {
            if let Ok (m) = right.subItems [name] {
                let dmut n = copy Style ();
                n:.merge (innerStyle);
                n:.merge (m);

                self.subItems [name] = n;
            }
        }

        for name, innerStyle in right.subItems {
            if name !in self.subItems {
                self.subItems [name] = innerStyle;
            }
        }
    }

}
