in loader;

mod ::keys;

use balder::core::{_, application::_};
use balder::interface::_;
use balder::math::_;

use std::{config,
          config::_,
          conv,
          conv::_,
          traits,
          stream,
          syntax::_};

use balder::interface::widget::{_,
                                text::_,
                                layout::_};

use balder::interface::loader::keys;

pub def WidgetKeys : keys::Keywords;

/**
 * GUI parser to parse a string content containing the description of widgets
 * */
pub record GUIParser {

    let dmut _manager : &WidgetManager;
    let _content : [c8];

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ======================================          CTOR          ======================================
     * ====================================================================================================
     * ====================================================================================================
     */

    pub self (dmut manager : &WidgetManager, content : [c8])
        with _manager = alias manager
        , _content = content
    {}

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ====================================          LOADING          =====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    /**
     * Load a root configuration and returns the loaded widget
     * */
    pub fn load (mut self)
        throws BalderError
    {
        let dmut lexer = copy Lexer!{c8} (self._content,
                                          tokens-> copy Tokens::__members__,
                                          comments-> copy Comments::__members__);

        let dmut widget = self:.readWidget (alias lexer);
        self._manager:.setContent (alias widget);
    }

    /**
     * Read the configuration of a widget
     * @returns: the created widget
     * */
    fn readWidget (mut self, dmut lexer : &Lexer!{c8})-> dmut &Widget
        throws BalderError
    {
        let kind = lexer:.next ();
        self:.readWidget (alias lexer, kind)
    }

    /**
     * Read the configuration of a widget
     * @params:
     *    - lexer: the lexer tokenizing the content
     *    - kind: the word read in the lexer defining the kind of widget to read
     * */
    fn readWidget (mut self, dmut lexer : &Lexer!{c8}, kind : ([c8], usize, usize))-> dmut &Widget
        throws BalderError
    {
        let name = lexer:.next ();
        self.verifyIdentifier (expand name);

        match kind._0 {
            Keywords::LINEAR_LAYOUT   => { return self:.readLinearLayout (name._0, alias lexer); }
            Keywords::GRID_LAYOUT     => { return self:.readGridLayout (name._0, alias lexer); }
            Keywords::FLOATING_LAYOUT => { return self:.readFloatingLayout (name._0, alias lexer); }
            Keywords::BOX             => { return self:.readBox (name._0, alias lexer); }
            Keywords::LABEL           => { return self:.readLabel (name._0, alias lexer); }
            Keywords::INPUT_TEXT      => { return self:.readInputText (name._0, alias lexer); }
            Keywords::BUTTON          => { return self:.readButton (name._0, alias lexer); }
            Keywords::SIZE_GRIP       => { return self:.readSizeGripLayout (name._0, alias lexer); }
            Keywords::TAB_LAYOUT      => { return self:.readTabLayout (name._0, alias lexer); }
        }

        throw self.unexpected (expand kind);
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * =================================          LINEAR-LAYOUT          ==================================
     * ====================================================================================================
     * ====================================================================================================
     */

    /**
     * Read the configuration of a linear layout
     * @params:
     *    - name: the name of the widget
     *    - lexer: the lexer tokenizing the content
     * @returns: a linear layout
     * */
    fn readLinearLayout (mut self, name : [c8], dmut lexer : &Lexer!{c8})-> dmut &Widget
        throws BalderError
    {
        let beg = self.readTokens (alias lexer, Tokens::LPAR, Tokens::LACC);
        let (cl, scrollable, vertical, scrollSpeed) = if beg.0 == Tokens::LPAR {
            let a = self:.readLinearLayoutHeader (alias lexer);
            self.readTokens (alias lexer, Tokens::LACC);
            a
        } else {
            ("", false, false, 2.f)
        };

        let dmut line = copy LinearLayout (alias self._manager, name, vertical-> vertical);
        {
            let mut level : u32 = 0;
            loop {
                let c = lexer:.next ();
                match c._0 {
                    Tokens::RACC => { break; }
                    _ => {
                        let dmut child = self:.readWidget (alias lexer, c);
                        line:.insertChild (alias child, level-> level);
                        level += 1;
                    }
                }
            }

            line:.setStyleClasses (cl);
            if scrollable {
                let dmut result = copy ScrollLayout (alias self._manager, name, alias line, scrollSpeed-> scrollSpeed);
                result:.setStyleClasses (cl);
                return alias result;
            }

            alias line
        } catch {
            b => {
                line:.dispose ();
                throw b;
            }
        }
    }

    /**
     * Read the header part of a linear layout
     * @params:
     *    - lexer: the lexer tokenizing the content
     * @returns:
     *    - .0: the style class
     *    - .1: true iif scrollable
     *    - .2: true iif vertical
     *    - .3: the speed of the scroll (iif .1)
     * */
    fn readLinearLayoutHeader (mut self, dmut lexer : &Lexer!{c8})-> ([c8], bool, bool, f32)
        throws BalderError
    {
        let mut cl : [c8] = [], mut vertical = false, mut scrollable = false, mut speed = 2.f;
        let mut readCl = false, mut readVert = false, mut readScroll = false, mut readSpeed = false;
        loop {
            let ident = lexer:.next ();
            match ident._0 {
                Tokens::RPAR => { break; }
                Keywords::CLASS => {
                    if readCl { throw self.unexpected (expand ident); }
                    readCl = true;

                    self.readTokens (alias lexer, Tokens::COLON);
                    cl = self.readString (alias lexer);

                    if (self.readTokens (alias lexer, Tokens::COMA, Tokens::RPAR)._0 == Tokens::RPAR) break;
                }
                Keywords::ORIENTATION => {
                    if readVert { throw self.unexpected (expand ident); }
                    readVert = true;

                    self.readTokens (alias lexer, Tokens::COLON);
                    let hor = self.readTokens (alias lexer, Keywords::VERTICAL, Keywords::HORIZONTAL);
                    vertical = (hor._0 == Keywords::HORIZONTAL);

                    if (self.readTokens (alias lexer, Tokens::COMA, Tokens::RPAR)._0 == Tokens::RPAR) break;
                }
                Keywords::SCROLLABLE => {
                    if readScroll { throw self.unexpected (expand ident); }
                    readScroll = true;

                    self.readTokens (alias lexer, Tokens::COLON);
                    let hor = self.readTokens (alias lexer, Keywords::TRUE, Keywords::FALSE);
                    if hor._0 == Keywords::TRUE { scrollable = true; }

                    if (self.readTokens (alias lexer, Tokens::COMA, Tokens::RPAR)._0 == Tokens::RPAR) break;
                }
                Keywords::SCROLL_SPEED => {
                    if readSpeed { throw self.unexpected (expand ident); }
                    readSpeed = true;

                    self.readTokens (alias lexer, Tokens::COLON);
                    speed = self.readFloat!{f32} (alias lexer);

                    if (self.readTokens (alias lexer, Tokens::COMA, Tokens::RPAR)._0 == Tokens::RPAR) break;
                }
                _ => {
                    throw self.unexpected (expand ident);
                }
            }
        }

        (cl, scrollable, vertical, speed)
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ==================================          GRID-LAYOUT          ===================================
     * ====================================================================================================
     * ====================================================================================================
     */

    /**
     * Read the configuration of a grid layout
     * @params:
     *    - name: the name of the widget
     *    - lexer: the lexer tokenizing the content
     * @returns: a grid layout
     * */
    fn readGridLayout (mut self, name : [c8], dmut lexer : &Lexer!{c8})-> dmut &GridLayout
        throws BalderError
    {
        let beg = self.readTokens (alias lexer, Tokens::LPAR, Tokens::LACC);
        let (cl, cols, rows) = if beg.0 == Tokens::LPAR {
            let a = self:.readGridLayoutHeader (alias lexer);
            self.readTokens (alias lexer, Tokens::LACC);
            a
        } else {
            ("", 1u32, 1u32)
        };

        let dmut lay = copy GridLayout (alias self._manager, name, cols, rows);
        {
            loop {
                let c = self.readTokens (alias lexer, Tokens::LPAR, Tokens::RACC);
                if c._0 == Tokens::RACC { break; }

                let co = self.readInt!{u32} (alias lexer);
                self.readTokens (alias lexer, Tokens::COMA);
                let ro = self.readInt!{u32} (alias lexer);
                self.readTokens (alias lexer, Tokens::RPAR);

                let dmut child = self:.readWidget (alias lexer);
                lay:.insertChild (co, ro, alias child);
            }

            lay:.setStyleClasses (cl);
            alias lay
        } catch {
            b => {
                lay:.dispose ();
                throw b;
            }
        }
    }

    /**
     * Read the header part of a grid layout
     * @params:
     *    - lexer: the lexer tokenizing the content
     * @returns:
     *    - .0: the style class
     *    - .1: the number of columns
     *    - .2: the number of rows
     * */
    fn readGridLayoutHeader (mut self, dmut lexer : &Lexer!{c8})-> ([c8], u32, u32)
        throws BalderError
    {
        let mut cl : [c8] = [], mut cols = 1u32, mut rows = 1u32;
        let mut readCl = false, mut readCols = false, mut readRows = false;

        loop {
            let ident = lexer:.next ();
            match ident._0 {
                Tokens::RPAR => { break; }
                Keywords::CLASS => {
                    if readCl { throw self.unexpected (expand ident); }
                    readCl = true;

                    self.readTokens (alias lexer, Tokens::COLON);
                    cl = self.readString (alias lexer);

                    if (self.readTokens (alias lexer, Tokens::COMA, Tokens::RPAR)._0 == Tokens::RPAR) break;
                }
                Keywords::COLUMNS => {
                    if readCols { throw self.unexpected (expand ident); }
                    readCols = true;

                    self.readTokens (alias lexer, Tokens::COLON);
                    cols = self.readInt!{u32} (alias lexer);

                    if (self.readTokens (alias lexer, Tokens::COMA, Tokens::RPAR)._0 == Tokens::RPAR) break;
                }
                Keywords::ROWS => {
                    if readRows { throw self.unexpected (expand ident); }
                    readRows = true;

                    self.readTokens (alias lexer, Tokens::COLON);
                    rows = self.readInt!{u32} (alias lexer);

                    if (self.readTokens (alias lexer, Tokens::COMA, Tokens::RPAR)._0 == Tokens::RPAR) break;
                }
                _ => {
                    throw self.unexpected (expand ident);
                }
            }
        }

        (cl, cols, rows)
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ================================          FLOATING-LAYOUT          =================================
     * ====================================================================================================
     * ====================================================================================================
     */

    /**
     * Read the configuration of a floating layout
     * @params:
     *    - name: the name of the widget
     *    - lexer: the lexer tokenizing the content
     * @returns: a floating layout
     * */
    fn readFloatingLayout (mut self, name : [c8], dmut lexer : &Lexer!{c8})-> dmut &FloatingLayout
        throws BalderError
    {
        let beg = self.readTokens (alias lexer, Tokens::LACC, Tokens::LPAR);

        let cl = if (beg._0 == Tokens::LPAR) {
            let a = self:.readFloatingLayoutHeader (alias lexer);
            self.readTokens (alias lexer, Tokens::LACC);
            a
        } else {
            ""
        };

        let dmut lay = copy FloatingLayout (alias self._manager, name);

        {
            let mut level : u32 = 0;
            loop {
                let c = self.readTokens (alias lexer, Tokens::LPAR, Tokens::RACC);
                if c._0 == Tokens::RACC { break; }

                let (x, y, events) = self.readFloatingChildPosition (alias lexer);

                let dmut child = self:.readWidget (alias lexer);
                lay:.insertChild (alias child, x-> x, y-> y, withEvents-> events, level-> level);
                level += 1;
            }
        }

        lay:.setStyleClasses (cl);

        alias lay
    }

/**
     * Read the header part of a floating layout
     * @params:
     *    - lexer: the lexer tokenizing the content
     * @returns:
     *    - .0: the style class
     * */
    fn readFloatingLayoutHeader (mut self, dmut lexer : &Lexer!{c8})-> [c8]
        throws BalderError
    {
        let mut cl : [c8] = [];
        let mut readCl = false;

        loop {
            let ident = lexer:.next ();
            match ident._0 {
                Tokens::RPAR => { break; }
                Keywords::CLASS => {
                    if readCl { throw self.unexpected (expand ident); }
                    readCl = true;

                    self.readTokens (alias lexer, Tokens::COLON);
                    cl = self.readString (alias lexer);

                    if (self.readTokens (alias lexer, Tokens::COMA, Tokens::RPAR)._0 == Tokens::RPAR) break;
                }
                _ => {
                    throw self.unexpected (expand ident);
                }
            }
        }

        cl
    }

    /**
     * Read the position of a floating widget
     * @params:
     *    - lexer: the lexer tokenizing the content
     * @returns:
     *    - .0: the x position
     *    - .1: the y position
     *    - .2: true iif the widget receive events
     * */
    fn readFloatingChildPosition (self, dmut lexer : &Lexer!{c8})-> (WidgetSize, WidgetSize, bool)
        throws BalderError
    {
        let mut x = WidgetSize (), mut y = WidgetSize (), mut events = true;
        let mut readX = false, mut readY = false, mut readE = false;
        loop {
            let c = lexer:.next ();
            match c._0 {
                Tokens::RPAR => { break; }
                Keywords::X => {
                    if readX { throw self.unexpected (expand c); }
                    readX = true;

                    self.readTokens (alias lexer, Tokens::COLON);
                    x = self.readSize (alias lexer);

                    if (self.readTokens (alias lexer, Tokens::COMA, Tokens::RPAR)._0 == Tokens::RPAR) break;
                }
                Keywords::Y => {
                    if readY { throw self.unexpected (expand c); }
                    readY = true;

                    self.readTokens (alias lexer, Tokens::COLON);
                    y = self.readSize (alias lexer);

                    if (self.readTokens (alias lexer, Tokens::COMA, Tokens::RPAR)._0 == Tokens::RPAR) break;
                }
                Keywords::WITH_EVENTS => {
                    if readE { throw self.unexpected (expand c); }
                    readE = true;

                    self.readTokens (alias lexer, Tokens::COLON);
                    let t = self.readTokens (alias lexer, Keywords::TRUE, Keywords::FALSE);
                    events = (t._0 == Keywords::TRUE);

                    if (self.readTokens (alias lexer, Tokens::COMA, Tokens::RPAR)._0 == Tokens::RPAR) break;
                }
                _ => {
                    throw self.unexpected (expand c);
                }
            }
        }

        (x, y, events)
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ======================================          BOX          =======================================
     * ====================================================================================================
     * ====================================================================================================
     */

    /**
     * Read the configuration of a grid layout
     * @params:
     *    - name: the name of the widget
     *    - lexer: the lexer tokenizing the content
     * @returns: a grid layout
     * @example:
     * ```
     * Box mybox (class: 'foo.bar');
     * ```
     * @grammar:
     * ```
     * box := 'Box' Identifier ('(' 'class' ':' String ')')? ';'
     * ```
     * */
    fn readBox (mut self, name : [c8], dmut lexer : &Lexer!{c8})-> dmut &Box
        throws BalderError
    {
        let config = self.readTokens (alias lexer, Tokens::SEMI_COLON, Tokens::LPAR);
        let dmut result = copy Box (alias self._manager, name);

        {
            if config._0 == Tokens::LPAR  {
                self.readTokens (alias lexer, Keywords::CLASS);
                self.readTokens (alias lexer, Tokens::COLON);

                let str = self.readString (alias lexer);
                self.readTokens (alias lexer, Tokens::RPAR);

                result:.setStyleClasses (str);

                self.readTokens (alias lexer, Tokens::SEMI_COLON);
            }
        } catch {
            b => {
                result:.dispose ();
                throw b;
            }
        }

        alias result
    }


    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ===================================          SIZE GRIP          ====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    /**
     * Read the configuration of a size grip
     * @params:
     *    - name: the name of the widget
     *    - lexer: the lexer tokenizing the content
     * @returns: a size grip
     * @example:
     * ```
     * SizeGrip mysizegrip (class: 'foo.bar') {
     *     Widget A;
     *     Widget B;
     * }
     * ```
     * @grammar:
     * ```
     * sizegrip := 'SizeGrip' Identifier ('(' 'class' ':' String ')')? ';'
     * ```
     * */
    fn readSizeGripLayout (mut self, name : [c8], dmut lexer : &Lexer!{c8})-> dmut &SizeGripLayout
        throws BalderError
    {
        let beg = self.readTokens (alias lexer, Tokens::LPAR, Tokens::LACC);
        let (cl, vertical) = if beg.0 == Tokens::LPAR {
            let a = self:.readSizeGripLayoutHeader (alias lexer);
            self.readTokens (alias lexer, Tokens::LACC);
            a
        } else {
            ("", false)
        };

        let dmut left = self:.readWidget (alias lexer);
        let dmut right = self:.readWidget (alias lexer);

        let dmut result = copy SizeGripLayout (alias self._manager, name, alias left, alias right, vertical);
        result:.setStyleClasses (cl);

        alias result
    }


    /**
     * Read the header part of a size grip layout
     * @params:
     *    - lexer: the lexer tokenizing the content
     * @returns:
     *    - .0: the style class
     *    - .1: true iif vertical
     * */
    fn readSizeGripLayoutHeader (mut self, dmut lexer : &Lexer!{c8})-> ([c8], bool)
        throws BalderError
    {
        let mut cl : [c8] = [], mut vertical = false;
        let mut readCl = false, mut readVert = false;
        loop {
            let ident = lexer:.next ();
            match ident._0 {
                Tokens::RPAR => { break; }
                Keywords::CLASS => {
                    if readCl { throw self.unexpected (expand ident); }
                    readCl = true;

                    self.readTokens (alias lexer, Tokens::COLON);
                    cl = self.readString (alias lexer);

                    if (self.readTokens (alias lexer, Tokens::COMA, Tokens::RPAR)._0 == Tokens::RPAR) break;
                }
                Keywords::ORIENTATION => {
                    if readVert { throw self.unexpected (expand ident); }
                    readVert = true;

                    self.readTokens (alias lexer, Tokens::COLON);
                    let hor = self.readTokens (alias lexer, Keywords::VERTICAL, Keywords::HORIZONTAL);
                    vertical = (hor._0 == Keywords::HORIZONTAL);

                    if (self.readTokens (alias lexer, Tokens::COMA, Tokens::RPAR)._0 == Tokens::RPAR) break;
                }
                _ => {
                    throw self.unexpected (expand ident);
                }
            }
        }

        (cl, vertical)
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ===================================          TAB LAYOUT          ===================================
     * ====================================================================================================
     * ====================================================================================================
     */


    /**
     * Read the configuration of a tab layout
     * @params:
     *    - name: the name of the widget
     *    - lexer: the lexer tokenizing the content
     * @returns: a linear layout
     * */
    fn readTabLayout (mut self, name : [c8], dmut lexer : &Lexer!{c8})-> dmut &Widget
        throws BalderError
    {
        let beg = self.readTokens (alias lexer, Tokens::LPAR, Tokens::LACC);
        let cl = if beg.0 == Tokens::LPAR {
            let a = self:.readTabLayoutHeader (alias lexer);
            self.readTokens (alias lexer, Tokens::LACC);
            a
        } else {
            ""
        };

        let dmut tab = copy TabLayout (alias self._manager, name);
        {
            let mut level : u32 = 0;
            loop {
                let c = self.readTokens (alias lexer, Tokens::LPAR, Tokens::RACC);
                match c._0 {
                    Tokens::RACC => {
                        // At least one child
                        if level == 0 { throw self.unexpected (expand c); }
                        break;
                    }
                    _ => {
                        let head = self.readString (alias lexer);
                        self.readTokens (alias lexer, Tokens::RPAR);

                        let dmut child = self:.readWidget (alias lexer);
                        tab:.insertTab (head, alias child);
                        level += 1;
                    }
                }
            }

            tab:.setStyleClasses (cl);
            alias tab

        } catch {
            b => {
                tab:.dispose ();
                throw b;
            }
        }
    }

    /**
     * Read the header part of a linear layout
     * @params:
     *    - lexer: the lexer tokenizing the content
     * @returns:
     *    - .0: the style class
     *    - .1: true iif scrollable
     *    - .2: true iif vertical
     *    - .3: the speed of the scroll (iif .1)
     * */
    fn readTabLayoutHeader (mut self, dmut lexer : &Lexer!{c8})-> [c8]
        throws BalderError
    {
        let mut cl : [c8] = [];
        let mut readCl = false;
        loop {
            let ident = lexer:.next ();
            match ident._0 {
                Tokens::RPAR => { break; }
                Keywords::CLASS => {
                    if readCl { throw self.unexpected (expand ident); }
                    readCl = true;

                    self.readTokens (alias lexer, Tokens::COLON);
                    cl = self.readString (alias lexer);

                    if (self.readTokens (alias lexer, Tokens::COMA, Tokens::RPAR)._0 == Tokens::RPAR) break;
                }
                _ => {
                    throw self.unexpected (expand ident);
                }
            }
        }

        cl
    }


    /*!
     * ====================================================================================================
     * ====================================================================================================
     * =====================================          LABEL          ======================================
     * ====================================================================================================
     * ====================================================================================================
     */

    /**
     * Read the configuration of a grid layout
     * @params:
     *    - name: the name of the widget
     *    - lexer: the lexer tokenizing the content
     * @returns: a grid layout
     * */
    fn readLabel (mut self, name : [c8], dmut lexer : &Lexer!{c8})-> dmut &Label
        throws BalderError
    {
        let config = self.readTokens (alias lexer, Tokens::LACC, Tokens::LPAR);
        let dmut lbl = copy Label (alias self._manager, name);

        {
            if config._0 == Tokens::LPAR  {
                self.readTokens (alias lexer, Keywords::CLASS);
                self.readTokens (alias lexer, Tokens::COLON);

                let str = self.readString (alias lexer);
                self.readTokens (alias lexer, Tokens::RPAR);

                lbl:.setStyleClasses (str);

                self.readTokens (alias lexer, Tokens::LACC);
            }
        } catch {
            b => {
                lbl:.dispose ();
                throw b;
            }
        }

        let content = self.readString (alias lexer);
        self.readTokens (alias lexer, Tokens::RACC);

        lbl:.setText (content);
        alias lbl
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ===================================          INPUT-TEXT          ===================================
     * ====================================================================================================
     * ====================================================================================================
     */

    /**
     * Read the configuration of a grid layout
     * @params:
     *    - name: the name of the widget
     *    - lexer: the lexer tokenizing the content
     * @returns: a grid layout
     * */
    fn readInputText (mut self, name : [c8], dmut lexer : &Lexer!{c8})-> dmut &InputText
        throws BalderError
    {
        let beg = self.readTokens (alias lexer, Tokens::SEMI_COLON, Tokens::LPAR);
        let (cl, max, numbers) = if beg._0 == Tokens::LPAR {
            let a = self.readInputTextHeader (alias lexer);
            self.readTokens (alias lexer, Tokens::SEMI_COLON);
            a
        } else {
            ("", 255, false)
        };

        let dmut res = copy InputText (alias self._manager, name, maxLen-> cast!usize (max), onlyNumbers-> numbers);
        res:.setStyleClasses (cl);

        alias res
    }

    /**
     * Read the header part of an input text
     * @returns:
     *    - .0: the style class
     *    - .1: the maximum number of chars in the input text
     *    - .2: true iif only accepts numbers
     * */
    fn readInputTextHeader (self, dmut lexer : &Lexer!{c8})-> ([c8], u32, bool)
        throws BalderError
    {
        let mut cl : [c8] = [], mut len = 255u32, mut numbers = false;
        let mut readCl = false, mut readL = false, mut readN = false;

        loop {
            let ident = lexer:.next ();
            match ident._0 {
                Tokens::RPAR => { break; }
                Keywords::CLASS => {
                    if readCl { throw self.unexpected (expand ident); }
                    readCl = true;

                    self.readTokens (alias lexer, Tokens::COLON);
                    cl = self.readString (alias lexer);

                    if (self.readTokens (alias lexer, Tokens::COMA, Tokens::RPAR)._0 == Tokens::RPAR) break;
                }
                Keywords::LENGTH => {
                    if readL { throw self.unexpected (expand ident); }
                    readL = true;

                    self.readTokens (alias lexer, Tokens::COLON);
                    len = self.readInt!{u32} (alias lexer);

                    if (self.readTokens (alias lexer, Tokens::COMA, Tokens::RPAR)._0 == Tokens::RPAR) break;
                }
                Keywords::ONLY_NUMBERS => {
                    if readN { throw self.unexpected (expand ident); }
                    readN = true;

                    self.readTokens (alias lexer, Tokens::COLON);
                    let t = self.readTokens (alias lexer, Keywords::TRUE, Keywords::FALSE);
                    numbers = (t._0 == Keywords::TRUE);

                    if (self.readTokens (alias lexer, Tokens::COMA, Tokens::RPAR)._0 == Tokens::RPAR) break;
                }
                _ => {
                    throw self.unexpected (expand ident);
                }
            }
        }

        (cl, len, numbers)
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * =====================================          BUTTON          =====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    /**
     * Read the configuration of a grid layout
     * @params:
     *    - name: the name of the widget
     *    - lexer: the lexer tokenizing the content
     * @returns: a grid layout
     * */
    fn readButton (mut self, name : [c8], dmut lexer : &Lexer!{c8})-> dmut &Button
        throws BalderError
    {
        let config = self.readTokens (alias lexer, Tokens::LACC, Tokens::LPAR);
        let dmut lbl = copy Button (alias self._manager, name);

        {
            if config._0 == Tokens::LPAR  {
                self.readTokens (alias lexer, Keywords::CLASS);
                self.readTokens (alias lexer, Tokens::COLON);

                let str = self.readString (alias lexer);
                self.readTokens (alias lexer, Tokens::RPAR);

                lbl:.setStyleClasses (str);

                self.readTokens (alias lexer, Tokens::LACC);
            }
        } catch {
            b => {
                lbl:.dispose ();
                throw b;
            }
        }

        let content = self.readString (alias lexer);
        self.readTokens (alias lexer, Tokens::RACC);

        lbl:.setText (content);
        alias lbl
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * =====================================          COMMON          =====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    /**
     * Read a string
     * */
    fn readString (self, dmut lexer : &Lexer!{c8})-> [c8]
        throws BalderError
    {
        let beg = self.readTokens (alias lexer, Tokens::SQUOTE, Tokens::DQUOTE);

        let dmut result = copy StringStream ();
        lexer:.setSkipTokens (false);
        {
            loop {
                let n = lexer:.next ();
                if n._0 == beg._0 { break; }
                result:.write (n._0);
            }
        } exit {
            lexer:.setSkipTokens (true);
        }

        result[]
    }

    /**
     * Read a float
     * */
    fn if isFloating!{T} readFloat {T} (self, dmut lexer : &Lexer!{c8})-> T
        throws BalderError
    {
        let pix = lexer:.next ();
        let cursor = lexer.getCursor ();

        let point = lexer:.next ();

        let mut result = cast!{T} (0.f);
        {
            if point._0 == Tokens::POINT {
                let end = lexer:.next ();
                result = std::conv::to!{T} (pix._0 ~ "." ~ end._0);
            } else {
                lexer:.rewind (expand cursor);
                result = std::conv::to!{T} (pix._0);
            }
        } catch {
            _ => { throw self.unexpected (expand pix); }
        }

        result
    }

    /**
     * Read a int
     * */
    fn if isIntegral!{T} readInt {T} (self, dmut lexer : &Lexer!{c8})-> T
        throws BalderError
    {
        let pix = lexer:.next ();

        let mut result = cast!{T} (0);
        {
            result = std::conv::to!{T} (pix._0);
        } catch {
            _ => { throw self.unexpected (expand pix); }
        }

        result
    }

    /**
     * Read a widget size
     * */
    fn readSize (self, dmut lexer : &Lexer!{c8})-> WidgetSize
        throws BalderError
    {
        let pix = lexer:.next ();
        if pix._0 == Tokens::LACC {
            return self.readComplexSize (alias lexer);
        }

        let mut size = {
            WidgetSize (pix-> std::conv::to!{u32} (pix._0))
        } catch {
            _ => {
                throw self.unexpected (expand pix);
            }
        };

        size
    }

    /**
     * Read a complex size
     * */
    fn readComplexSize (self, dmut lexer : &Lexer!{c8})-> WidgetSize
        throws BalderError
    {
        let mut result = WidgetSize ();
        loop {
            let (n, l, c) = lexer:.next ();
            match n {
                Tokens::RACC => { break; }
                Keywords::RELATIVE => {
                    self.readTokens (alias lexer, Tokens::COLON);
                    result.relative = self.readFloat!{f32} (alias lexer);
                    self.readTokens (alias lexer, Tokens::SEMI_COLON);
                }
                Keywords::MIN => {
                    self.readTokens (alias lexer, Tokens::COLON);
                    result.minPix = self.readInt!{u32} (alias lexer);
                    self.readTokens (alias lexer, Tokens::SEMI_COLON);
                }
                Keywords::MAX => {
                    self.readTokens (alias lexer, Tokens::COLON);
                    result.maxPix = self.readInt!{u32} (alias lexer);
                    self.readTokens (alias lexer, Tokens::SEMI_COLON);
                }
                _ => {
                    throw self.unexpected (n, l, c);
                }
            }
        }

        result
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ====================================          CHECKING          ====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    /**
     * Read the next token in the lexer and check that it correspond to one of the expected tokens
     * */
    fn readTokens {T...} (self, dmut lexer : &Lexer!{c8}, expect : T)-> ([c8], usize, usize)
        throws BalderError
    {
        let n = lexer:.next ();
        cte for j in expect {
            if n._0 == j { return n; }
        }

        throw self.unexpected (expand n);
    }

    /**
     * Read the next token in the lexer and check that it correspond to one of the expected tokens
     * */
    fn readTokens (self, dmut lexer : &Lexer!{c8}, expect : [c8])-> ([c8], usize, usize)
        throws BalderError
    {
        let n = lexer:.next ();
        if expect == n._0 {
            return n;
        }

        throw self.unexpected (expand n);
    }

    /**
     * Throw an unexpected token exception
     * @params:
     *    - tok: the read token
     *    - line: the line of the reading
     *    - col: the column of the reading
     * */
    fn unexpected (self, tok : [c8], line : usize, col : usize)-> &BalderError {
        let msg = if let Ok (m) = (std::format::format ("Unexpected '{}' at ({}:{})", tok, line, col))? {
            m
        } else {
            ("Unexpected '" ~ tok ~ "'")
        };

        copy BalderError (msg)
    }

    /**
     * Verify that the name is a identifier
     * */
    fn verifyIdentifier (self, name : [c8], line : usize, col : usize)
        throws BalderError
    {
        if !self.isIdentifier (name) {
            let msg = if let Ok (m) = (std::format::format ("Invalid identifier '{}' at ({}:{})", name, line, col))? {
                m
            } else {
                ("Invalid identifier '" ~ name ~ "'")
            };

            throw copy BalderError (msg);
        }
    }

    /**
     * @returns: true if name is an identifier
     * */
    fn isIdentifier (self, str : [c8])-> bool {
        let mut i = 0u64;
        let mut found = false;
                for j in str { // must start by a letter, but can be preceded by as many _ as wanted
            if (j >= 'a' && j <= 'z') || (j >= 'A' && j <= 'Z') {
                found = true;
                break;
            } else if (j != '_' && j != '-') {
                return false;
            }
            i += 1u64;
        }

        i += 1u64;
        if (i < str.len) { // the rest of the identifier must be composed of letters, numbers, or _
            for j in str [cast!usize (i) .. $] {
                if (j < 'a' || j > 'z') && (j < 'A' || j > 'Z') && (j != '_') && (j < '0' || j > '9') && (j != '-') {
                    return false;
                }
            }
        } else {
            return found;
        }

        true
    }

}
