in drawer;

use balder::core::_;
use balder::core::{_,
                   shader::_,
                   application::_,
                   driver::_,
                   config::_};

use balder::{utils::_, math::_};

use balder::core::application::drawer::_;

@final
pub class ShapeDrawer {

    pub let dmut _app : &Application;

    let dmut _colorShader : (&Shader)? = none;
    let dmut _textureShader : (&Shader)? = none;

    let dmut _quadColorVBO = copy VertexBufferObject ();
    let dmut _quadTextureVBO = copy VertexBufferObject ();
    let dmut _quadIBO = copy IndexBufferObject!{u16} ();

    let dmut _colorShapes : [usize => &Shape] = copy [];
    let dmut _textureShapes : [usize => &Shape] = copy [];

    let mut _colorToDraw : [usize => mut &Shape] = copy [];
    let mut _textToDraw : [usize => mut &Shape] = copy [];

    let dmut _uid = 1us;


    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ===================================          CTOR/DTOR          ====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    pub self (dmut app : &Application)
        with _app = alias app
    {}

    __dtor (mut self) {
        self:.dispose ();
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * =================================          CONFIGURATION          ==================================
     * ====================================================================================================
     * ====================================================================================================
     */

    /**
     * Configure the widget drawer on the given window
     * */
    pub fn configure (mut self)
        throws BalderError
    {
        let dmut c = copy RasterShader (alias self._app:.getWindow ():.getVulkanDevice (), config::shader::BASE_SHADER_COLOR_2D);
        self._colorShader = (alias c)?;

        let dmut t = copy RasterShader (alias self._app:.getWindow ():.getVulkanDevice (), config::shader::BASE_SHADER_TEXTURE_2D);
        self._textureShader = (alias t)?;

        self._quadIBO:.update (alias self._app:.getWindow ():.getVulkanDevice (), copy [1u32, 0u32, 2u32,
                                                                                        2u32, 0u32, 3u32]);

        self._quadColorVBO:.update (alias self._app:.getWindow ():.getVulkanDevice (),
                                    4us,
                                    copy [-1.f, -1.f,
                                          -1.f, 1.f,
                                          1.f, 1.f,
                                          1.f, -1.f]);

        self._quadTextureVBO:.update (alias self._app:.getWindow ():.getVulkanDevice (),
                                      4us,
                                      copy [-1.f, -1.f, 0.0f, 0.0f,
                                            -1.f, 1.f, 0.0f, 1.0f,
                                            1.f, 1.f, 1.0f, 1.0f,
                                            1.f, -1.f, 1.0f, 0.0f]);

        self._app:.getWindow ():.setComposePass (&self:.loadPhase, &self:.drawPhase);
        self._app:.getInputManager ():.connectResize (&self:.onWindowResize);
    } catch {
        err => {
            log::error ("Failed to create widget drawer", err);
            throw err;
        }
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * =====================================          ADDING          =====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    /**
     * Register a color quad in the window to draw
     * */
    pub fn registerQuad (mut self, position : vec2, color : vec4, size : vec2, rotation : f32 = 0.f, radius : f32 = 0.f)-> dmut &Shape
        throws BalderError
    {
        atomic self {
            if let Ok (dmut sh) = alias self._colorShader {
                let dmut result = copy Shape (self._uid, alias self, alias sh, self._quadColorVBO, self._quadIBO, none);
                self._uid += 1;

                let dim = self._app:.getWindow ().getDimension ();
                result:.setWindowDimension (makeVec2 (cast!f32 (dim._0), cast!f32 (dim._1)));
                result:.setPosition (position);
                result:.setSize (size);
                result:.setRotation (rotation);
                result:.setColor (color);
                result:.setRadius (radius);
                result:.setLevel (1);

                self._colorShapes [result.uid] = alias result;
                self:.redraw ();
                return alias result;
            }
        }

        throw copy BalderError ("Invalid shape drawer");
    }

    /**
     * Register a textured quad in the window to draw
     * */
    pub fn registerTextureQuad (mut self, position : vec2, color : vec4, size : vec2, texture : (&Texture)?, rotation : f32 = 0.f, radius : f32 = 0.f)-> dmut &Shape
        throws BalderError
    {
        atomic self {
            if let Ok (dmut sh) = alias self._textureShader {
                let dmut result = copy Shape (self._uid, alias self, alias sh, self._quadTextureVBO, self._quadIBO, texture);
                self._uid += 1;

                let dim = self._app:.getWindow ().getDimension ();
                result:.setWindowDimension (makeVec2 (cast!f32 (dim._0), cast!f32 (dim._1)));
                result:.setPosition (position);
                result:.setSize (size);
                result:.setRotation (rotation);
                result:.setColor (color);
                result:.setRadius (radius);
                result:.setLevel (1);

                self._textureShapes [result.uid] = alias result;
                self:.redraw ();
                return alias result;
            }
        }

        throw copy BalderError ("Invalid shape drawer");
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ====================================          DRAWING          =====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    /**
     * Force a redrawing due to an update in shapes
     * */
    pub fn redraw (mut self) {
        self._app:.getWindow ():.redraw ()
    }

    /**
     * Load phase of the widget drawing
     * */
    fn loadPhase (mut self, dmut cmd : &IndirectCommandAllocator) {
        atomic self {
            let mut i = 0;
            self._colorToDraw = copy [];
            for u, dmut c in alias self._colorShapes if !c.isHidden () {
                c:.register (alias cmd);
                self._colorToDraw [u] = c;
                i += 1;
            }

            self._textToDraw = copy [];
            for u, dmut c in alias self._textureShapes if !c.isHidden () {
                if let Ok (_) = c.getTexture () {
                    c:.register (alias cmd);
                    self._textToDraw [u] = c;
                    i += 1;
                }
            }
        }
    }

    /**
     * Draw phase of the widget drawing
     * */
    fn drawPhase (mut self, dmut cmd : &IndirectCommandAllocator) {
        atomic self {
            if let Ok (dmut sh) = alias self._colorShader {
                sh.select ();
                for _, c in self._colorToDraw {
                    c.draw (alias sh, alias cmd);
                }
            }

            if let Ok (dmut sh) = alias self._textureShader {
                sh.select ();
                for _, c in self._textToDraw {
                    c.draw (alias sh, alias cmd);
                }
            }
        }
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ====================================          TRIGGERS          ====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    /**
     * Triggered when the window change size
     * */
    fn onWindowResize (mut self, x : u32, y : u32) {
        atomic self {
            for _, dmut c in alias self._colorShapes {
                c:.setWindowDimension (makeVec2 (cast!f32 (x), cast!f32 (y)));
            }

            for _, dmut c in alias self._textureShapes {
                c:.setWindowDimension (makeVec2 (cast!f32 (x), cast!f32 (y)));
            }
        }
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ====================================          CLEARING          ====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    /**
     * Clear all loaded widgets
     * */
    pub fn clear (mut self) {
        atomic self {
            for _, dmut c in alias self._colorShapes {
                c:.dispose ();
            }

            for _, dmut c in alias self._textureShapes {
                c:.dispose ();
            }

            self._colorShapes = copy [];
            self._textureShapes = copy [];
        }
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ===================================          DISPOSING          ====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    impl Disposable {
        pub over dispose (mut self) {
            self:.clear ();
            atomic self {
                if let Ok (dmut c) = alias self._colorShader {
                    c:.dispose ();
                }

                if let Ok (dmut c) = alias self._textureShader {
                    c:.dispose ();
                }

                self._quadColorVBO:.dispose ();
                self._quadTextureVBO:.dispose ();
                self._quadIBO:.dispose ();
            }
        }
    }

}
