/**
 * @authors: Emile Cadorel
 */
in application;

pub mod ::activity;
pub mod ::input;
pub mod ::window;
pub mod ::timer;
pub mod ::refcount;
pub mod ::bin;
pub mod ::signal;

mod ::pass;

use std::{config::_,
          fs::_,
          io,
          time::_,
          syntax::_};

use balder::utils::_;
use balder::core::{_, application::_};
use balder::core::driver::_;

/**
 * Main part of a Balder application
 * @example:
 * ===
 * // Create an application from toml configuration file
 * // Config path is relative to `${pwd}/res/`
 * let dmut app = copy Application (Path ("path/to/config.toml"));
 * app:.run (); // start the application with the fnault activity
 * app:.dispose (); // Application is disposable
 * ===
 * @info: application can handle only one window
 */
@final
pub class Application {

    // The timer to cap the activity update frequency
    let mut _timer = Timer ();

    // The duration of a step of the application (one update of the activity)
    let mut _stepDuration = dur::millis (16u64) + dur::micros (666u64);

    // While _isRunning is true the app is running
    let mut _isRunning : bool = true;
    
    // The list of input manager for each activities
    let dmut _input : &InputManager = copy InputManager ();

    prot { // activities

        // The list of activities that controlling the application
        let dmut _running : [usize => (&Activity, &Window)] = copy [];

        // The list of activity opened but not started
        let dmut _opened : [usize => (&Activity, &Window)] = copy [];

        // The list of activities to close (in running)
        let dmut _closed : [usize => ()] = copy [];

        // The uid of the last open activity
        let mut _lastActId : usize = 0us;

    }

    /**
     * ======================================================================================================
     * ======================================================================================================
     * =======================================   CTOR AND CONFIGURE  ========================================
     * ======================================================================================================
     * ======================================================================================================
     */

    /**
     * Create a new application
     * @params:
     *    - ips: the duration of a step between activity updates
     */
    pub self (ips : u64 = 144) {
        self._stepDuration = dur::seconds (1) / ips;
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * =================================          OPEN ACTIVITY          ==================================
     * ====================================================================================================
     * ====================================================================================================
     */

    /**
     * Open a new activity
     * @params:
     *    - path: the path to the configuration file of the activity
     * */
    pub fn open {T over &Activity} (mut self, path : [c8])-> dmut T
        throws BalderError
    {
        self:.open!{T} (copy BalderConfig (path))
    }

    /**
     * Open a new activity
     * @params:
     *    - cfg: the configuration of the window
     * */
    pub fn open {T over &Activity} (mut self, cfg : &BalderConfig)-> dmut T
        throws BalderError
    {
        atomic self {
            self._lastActId += 1;
            let uid = self._lastActId;

            let dmut win = self:.openWindow (cfg);
            let dmut act = copy T (uid, alias self, alias win);
            self._opened [uid] = (alias act, alias win);

            alias act
        }
    }

    /**
     * Close a running activity
     * */
    pub fn close (mut self, dmut t : &Activity) {
        atomic self {
            self._closed [t.uid] = ();
        }
    }

    /**
     * Configure the application with a given configuration
     * @params:
     *    - conf: the configuration of the window
     */
    fn openWindow (mut self, cfg : &BalderConfig)-> dmut &Window
        throws BalderError
    {
        let dmut win = copy Window (alias self);

        win:.setDimension (cfg.window.width, cfg.window.height);
        win:.setName (cfg.window.name);
        win:.configureVulkan (cfg);

        win:.configureInputEvents ();

        alias win
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ====================================          FREEZING          ====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    /**
     * Freeze the input events of all opened windows (except rendering events)
     * @params:
     *    - capture: the function called when input keyboard or mouse event occurs but are freezed out in opened windows
     * */
    pub fn freezeInputEvents (mut self, dmut box : &SlotEmitter, capture : dg ()-> void) {
        for _, dmut r in alias self._running {
            atomic r._0 {
                r._1:.getInputManager ():.freeze (alias box, capture);
            }
        }
    }

    /**
     * Resume the input events of all opened windows
     * */
    pub fn resumeInputEvents (mut self) {
        for _, dmut r in alias self._running {
            atomic r._0 {
                r._1:.getInputManager ():.resume ();
            }
        }
    }

    /**
     * ======================================================================================================
     * ======================================================================================================
     * ================================================   USAGE    ==========================================
     * ======================================================================================================
     * ======================================================================================================
     */
    /**
     * Start the application
     */
    pub fn run (mut self) {
        self._timer:.reset ();
        self._input:.pollEvents ();

        while self._isRunning {
            self:.performActivityOpening ();

            // No need for atomic, this collection cannot be changed within this call
            for _, dmut w in alias self._running {
                w._0:.iterate ();
            }

            self._input:.pollEvents ();
            self:.performActivityClose ();
            self:.waitClock ();
        }

        self:.dispose ();
    }

    /**
     * Open the activity opened in previous iteration
     * */
    fn performActivityOpening (mut self) {
        if self._opened.len != 0 {
            let dmut toOpen = atomic self {
                let dmut res = alias self._opened;
                self._opened = copy [];
                alias res
            };

            for _, dmut t in alias toOpen {
                let (dmut a, dmut w) = alias t;
                a:.start ();
                w:.show ();

                // No need for atomic, this is only called in run iteration
                self._running [a.uid] = (alias a, alias w);
            }
        }
    }

    /**
     * Close the activities that are marked to be closed
     * */
    fn performActivityClose (mut self) {
        if self._closed.len != 0 {
            let toClose = atomic self {
                let res = self._closed;
                self._closed = copy [];
                res
            };

            for uid in toClose {
                if let Ok ((dmut act, dmut win)) = alias self._running [uid] {
                    act:.close ();

                    act:.dispose ();
                    win:.dispose ();

                    // No need for atomic, this is only called in run iteration
                    self._running:.remove (uid);
                }

                log::debug #("Application", "Closed activity :", uid);
            }

            // Quit when no activity are left
            if self._running.len == 0us {
                self:.quit ();
            }
        }
    }

    /**
     * Close the application
     */
    pub fn quit (mut self) {
        self._isRunning = false;
    }
    
    /**
     * Wait until the next update of the activity should be called.
     * @info: update the clock count.
     */
    prv fn waitClock (mut self) {
        let tick = self._timer.timeSinceStart ();
        if (self._stepDuration > tick) {
            let x = Instant::now ();
            let r = self._stepDuration - tick - dur::micros (1);

            sleep (r);
            let z = (Instant::now () - x) - r;
            self._timer:.reset (z);
        } else {
            let z = tick - self._stepDuration;
            self._timer:.reset (z);
        }
    }

    /**
     * ======================================================================================================
     * ======================================================================================================
     * ================================================   GETTERS    ========================================
     * ======================================================================================================
     * ======================================================================================================
     */

    /**
     * @returns: the input manager of the current activity
     */
    pub fn getInputManager (mut self) -> dmut &InputManager {
        alias self._input
    }

    /**
     * @returns: the input manager of the current activity
     */
    pub fn getInputManager (self) -> &InputManager {
        self._input
    }

    /**
     * ======================================================================================================
     * ======================================================================================================
     * =============================================   DISPOSING    =========================================
     * ======================================================================================================
     * ======================================================================================================
     */
    
    impl Disposable {
        pub over dispose (mut self) {
            for _, dmut w in alias self._opened {
                w._0:.dispose ();
                w._1:.dispose ();
            }

            for uid in self._running {
                self._closed [uid] = ();
            }

            self:.performActivityClose ();

            self._opened = copy [];
            self._running = copy [];
            self._closed = copy [];
        }        
    }

    /**
     * Call dispose
     */
    __dtor (mut self) {
        self:.dispose ();
    }
    
}

