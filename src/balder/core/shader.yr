in shader;


pub mod ::buffers;
pub mod ::layout;
pub mod ::desc_set;

pub mod ::texture;

mod ::raster;
mod::compute;

use balder::core::{_,
                   application::_,
                   config::_,
                   shader::_,
                   driver::_};

use balder::math::vector;

use ::vulkan::core;
use std::{conv, fs::_};

/**
 * Ancestor of all kind of shaders
 */
@abstract
pub class Shader {

    // The vulkan device holding the shader
    let dmut _device : &VulkanDevice;

    // The layout of the shader
    let dmut _pipelineLayout : &PipelineLayout;

    // The configuration of the shader
    let mut _uniformInfos : ([[c8] => UniformInfo])? = none;

    // The configuration of the shader
    let mut _textureInfos : ([[c8] => TextureInfo])? = none;

    // The configuration of the shader
    let mut _textureStorageInfos : ([[c8] => TextureInfo])? = none;

    // The vulkan handle
    let mut _program : VkPipeline = null;


    /*!
     * ====================================================================================================
     * ====================================================================================================
     * =====================================          CTORS          ======================================
     * ====================================================================================================
     * ====================================================================================================
     */

    pub self (dmut device : &VulkanDevice)
        with _device = alias device
        , _pipelineLayout = copy PipelineLayout (alias device)
    {}

    __dtor (mut self) {
        self:.dispose ();
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ====================================          GETTERS          =====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    /**
     * @returns: the device used by the shader
     */
    pub fn getDevice (mut self)-> dmut &VulkanDevice {
        alias self._device
    }

    /**
     * @returns: the pipeline layout of the shader
     */
    pub fn getPipelineLayout (mut self)-> dmut &PipelineLayout {
        alias self._pipelineLayout
    }

    /**
     * @returns: the device used by the shader
     */
    pub fn getDevice (self)-> &VulkanDevice {
        self._device
    }

    /**
     * @returns: the pipeline layout of the shader
     */
    pub fn getPipelineLayout (self)-> &PipelineLayout {
        self._pipelineLayout
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ================================          UNIFORM/TEXTURES          ================================
     * ====================================================================================================
     * ====================================================================================================
     */

    /**
     * @returns: the information about an uniform field
     * */
    pub fn getUniformInfo (self, name : [c8])-> (UniformInfo)? {
        if let Ok (cfg) = self._uniformInfos {
            return cfg [name];
        }

        none
    }

    /**
     * @returns: the information about an texture sampler field
     * */
    pub fn getTextureInfo (self, name : [c8])-> (TextureInfo)? {
        if let Ok (cfg) = self._textureInfos {
            return cfg [name];
        }

        none
    }

    /**
     * @returns: the list of textures
     * */
    pub fn getTextureInfos (self)-> ([[c8] => TextureInfo])? {
        if let Ok (cfg) = self._textureInfos {
            return cfg?;
        }

        none
    }

    /**
     * @returns: the information about an texture storage field
     * */
    pub fn getTextureStorageInfo (self, name : [c8])-> (TextureInfo)? {
        if let Ok (cfg) = self._textureStorageInfos {
            return cfg [name];
        }

        none
    }

    /**
     * @returns: the list of textures
     * */
    pub fn getTextureStorageInfo (self)-> ([[c8] => TextureInfo])? {
        if let Ok (cfg) = self._textureStorageInfos {
            return cfg?;
        }

        none
    }

    
    /**
     * @returns: the number of image sampler in the shader
     * */
    pub fn getNbTextures (self)-> usize {
        if let Ok (cfg) = self._textureInfos {
            return cfg.len;
        }

        0
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ====================================          UNIFORM          =====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    /**
     * Allocate a descriptor set to bind uniform values to the shader
     * */
    pub fn allocateDescriptorSet (mut self)-> dmut &DescriptorSet
        throws BalderError
    {
        let (poolId, dmut sets) = alias self._device:.getDescriptorAllocator ():.allocate (self._pipelineLayout.getDescriptorSetLayout (),
                                                                                           self.getDescriptorPoolSize ());

        let dmut s = copy DescriptorSet (alias self, poolId, alias sets);
        s:.createSamplers ();

        alias s
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ===================================          SELECTION          ====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    /**
     * Use the shader for the next drawings
     */
    pub fn select (self) {
        vkCmdBindPipeline (self._device.getCommandBuffer (), VkPipelineBindPoint::VK_PIPELINE_BIND_POINT_GRAPHICS, self._program);
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * =================================          CONFIGURATION          ==================================
     * ====================================================================================================
     * ====================================================================================================
     */

    fn getDescriptorPoolSize (self)-> [VkDescriptorType] {
        let dmut res : [VkDescriptorType] = [];

        if let Ok (cfg) = self._uniformInfos {
            res ~= copy [VkDescriptorType::VK_DESCRIPTOR_TYPE_UNIFORM_BUFFER ; cfg.len];
        }

        if let Ok (cfg) = self._textureInfos {
            res ~= copy [VkDescriptorType::VK_DESCRIPTOR_TYPE_COMBINED_IMAGE_SAMPLER ; cfg.len];
        }

        res
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * =================================          MISCELLANEAOUS          =================================
     * ====================================================================================================
     * ====================================================================================================
     */
    
    /**
     * @returns: the content of a shader file
     */
    prot fn readFile (self, src : Path)-> [u8]
        throws BalderError
    {
        {
            let dmut f = File::open (src);
            f:.readBytesAll ()
        } catch {
            _ => throw copy BalderError ("Shader file not found : " ~ src.toStr ());
        }
    }

    /**
     * Create a shader module from bytecode of the shader
     * @returns: the shader module
     */
    prot fn createShaderModule (mut self, bytecode : [u8])-> VkShaderModule
        throws BalderError
    {
        let mut createInfo = VkShaderModuleCreateInfo ();
        createInfo.sType = VkStructureType::VK_STRUCTURE_TYPE_SHADER_MODULE_CREATE_INFO;
        createInfo.codeSize = bytecode.len;
        createInfo.pCode = cast!{*u32} (cast!{*void} (bytecode.ptr));

        let dmut module : VkShaderModule = null;
        if (vkCreateShaderModule (self._device.getDeviceHandle (), &createInfo, null, alias &module) != VkResult::VK_SUCCESS) {
            throw copy BalderError ("Failed to create shader module");
        }

        module
    }
    
    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ===================================          DISPOSING          ====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    impl Disposable;

}
   

pub def RasterShader : raster::RasterShader;
pub def ComputeShader : compute::ComputeShader;

