in buffer;

use balder::core::{memory, memory::_, driver::device};
use balder::core::{error, dispose};
use balder::utils::log;

use ::vulkan::core;

use ::etc::c::memory;

@final
pub class Buffer {

    // The uniq id of the buffer
    pub let uid : usize;

    // The device on which the buffer is allocated
    let dmut _device : &VulkanDevice;

    // The allocator used to allocate the buffer memory
    let dmut _allocator : &MemoryAllocator;

    // The buffer handle
    let dmut _buffer : VkBuffer = null;

    // The memory allocation of the buffer (description used by MemoryAllocator)
    let dmut _memAlloc : MemoryAllocation;

    // The descriptor of the buffer
    let dmut _descriptor : VkDescriptorBufferInfo = VkDescriptorBufferInfo ();

    // True if the buffer is local to host
    let _isLocal : bool;

    /**
     * ======================================================================================================
     * ======================================================================================================
     * =======================================   CTOR AND CONFIGURE  ========================================
     * ======================================================================================================
     * ======================================================================================================
     */

    /**
     * Create a buffer using a device
     * @params:
     *    - device: the device used for the allocation
     *    - size: the size of the allocation
     *    - usage: the usage of the memory that will be allocated
     */
    pub self (uid : usize, dmut alloc : &MemoryAllocator, memAlloc : MemoryAllocation, dmut buffer : VkBuffer, isLocal : bool)
        with uid = uid
        , _allocator = alias alloc
        , _device = alloc:.getDevice ()
        , _buffer = alias buffer
        , _memAlloc = memAlloc
        , _descriptor = VkDescriptorBufferInfo ()
        , _isLocal = isLocal
    {
        self._descriptor.buffer = buffer;
        self._descriptor.offset = 0;
        self._descriptor.range = cast!u64 (memAlloc.size);
    }

    /**
     * ======================================================================================================
     * ======================================================================================================
     * ================================================   GETTERS  ==========================================
     * ======================================================================================================
     * ======================================================================================================
     */

    /**
     * @returns: the handle on the vulkan buffer
     */
    pub fn getVulkanBuffer (self)-> VkBuffer {
        self._buffer
    }

    /**
     * @returns: the device responsible of the allocation of the buffer
     */
    pub fn getDevice (mut self)-> dmut &VulkanDevice {
        alias self._device
    }

    /**
     * @returns: the device responsible of the allocation of the buffer
     */
    pub fn getDevice (self)-> &VulkanDevice {
        self._device
    }

    /**
     * @returns: the size of the buffer in memory
     */
    pub fn getSize (self)-> usize {
        self._memAlloc.size
    }

    pub fn getMapAddress (mut self)-> dmut *void {
        return self._allocator:.getMapAddress (self._memAlloc);
    }

    /**
     * @returns: the address of the descriptor buffer info
     */
    pub fn getDescriptorInfo (mut self)-> dmut *VkDescriptorBufferInfo {
        alias &self._descriptor
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * =================================          LOCAL MAPPING          ==================================
     * ====================================================================================================
     * ====================================================================================================
     */

    /**
     * Map the buffer onto CPU memory , so it can be updated
     * @assume: the buffer was not disposed
     */
    pub fn map (mut self)-> dmut *void {
        self._allocator:.getMapAddress (self._memAlloc)
    }

    /**
     * Unmap the address of the buffer
     * */
    pub fn unmap (mut self) {
        self._allocator:.unmap (self._memAlloc);
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ==================================          LOCAL COPIES          ==================================
     * ====================================================================================================
     * ====================================================================================================
     */

    /**
     * Copy the data on a local buffer
     * @warning: unlike self:.update (), this does not unmap at the end of the copy
     * */
    pub fn copyMappedData {T of [U], U} (mut self, datas : T) {
        atomic self._device {
            let dmut mptr = self:.map ();
            if (mptr != null) {
                memcpy (mptr, cast!{*void} (datas.ptr), datas.len * U::size);
                self:.unmap ();
            }
        }
    }

    /**
     * Copy the data on a local buffer
     * @warning: unlike self:.update (), this does not unmap at the end of the copy
     * */
    pub fn copyMappedData {T} (mut self, datas : *T) {
        atomic self._device {
            let dmut mptr = self:.map ();
            if (mptr != null) {
                memcpy (mptr, cast!{*void} (datas), T::size);
                self:.unmap ();
            }
        }
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ============================          AUTO LOCAL/REMOTE COPIES          ============================
     * ====================================================================================================
     * ====================================================================================================
     */

    /**
     * Update the buffer from CPU datas
     */
    pub fn update {T of [U], U} (mut self, datas : T)
        throws BalderError
    {
        if (self._isLocal) {
            atomic self._device {
                let dmut mptr = self:.map ();
                if (mptr != null) {
                    memcpy (mptr, cast!{*void} (datas.ptr), datas.len * U::size);
                    self:.unmap ();
                } else throw copy BalderError ("Failed to map memory");
            }
        } else {
            let dmut stagingBuf = self._device:.getMemoryAllocator ():.allocBuffer (
                cast!usize (self._memAlloc.size),
                cast!u32 (VkBufferUsageFlagBits::VK_BUFFER_USAGE_TRANSFER_SRC_BIT),
                cast!u32 (VkMemoryPropertyFlagBits::VK_MEMORY_PROPERTY_HOST_COHERENT_BIT |
                          VkMemoryPropertyFlagBits::VK_MEMORY_PROPERTY_HOST_VISIBLE_BIT));

            atomic self._device {
                let dmut mptr = stagingBuf:.map ();
                memcpy (mptr, cast!{*void} (datas.ptr), datas.len * U::size);
                stagingBuf:.unmap ();
            }

            // COPY 
            self._device:.getTransferPass ():.add (
                callback-> copy |cmd| { 
                    let mut copyRegion = VkBufferCopy ();

                    copyRegion.srcOffset = 0u64;
                    copyRegion.dstOffset = 0u64;
                    copyRegion.size = cast!u64 (self._memAlloc.size);
                    
                    vkCmdCopyBuffer (cmd, stagingBuf._buffer, self._buffer, 1u32, &copyRegion);
                },
                clean-> copy || {
                    stagingBuf:.dispose ();
                });                                    
        }
    }

    /**
     * ======================================================================================================
     * ======================================================================================================
     * ==============================================   DISPOSING  ==========================================
     * ======================================================================================================
     * ======================================================================================================
     */

    impl Disposable {

        pub over dispose (mut self) {
            if (self._buffer != null) {
                self._allocator:.freeBuffer (alias self._buffer, self._memAlloc);

                self._buffer = null;
                self._memAlloc = MemoryAllocation ();
            }
        }
    }

    __dtor (mut self) {
        self:.dispose ();
    }

}
