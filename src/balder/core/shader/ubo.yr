in ubo;

use balder::core::{error, dispose, memory::_, driver::_};
use vulkan::_;

/**
 * Ancestor of all uniform buffers
 */
@abstract
pub class UniformBufferObject {

    // The shader associated to the ubo
    let dmut _shader : &Shader;

    // The descriptor set associated to the ubo
    let dmut _descSet : &DescriptorSet;

    // The buffers containing the uniform values, one per frame in flight
    let mut _uniformBuffers : [dmut &Buffer] = [];

    /**
     * Create an empty ubo
     */
    prot self (dmut shader : &Shader, size : usize)
        with _shader = alias shader
        , _descSet = shader:.getAllocator ():.allocate (size)

        throws BalderError
    {}


    pub fn select (mut self) {
        self._descSet:.select ();
    }

    pub fn update {record UN} (mut self, data : UN) {
        if let dmut u : &UniformBufferObject!{UN} = alias self {
            u:.update (data);
        }
    }

    impl Disposable {
        pub over dispose (mut self) {
            for dmut uni in alias self._uniformBuffers {
                uni:.dispose ();
            }

            self._uniformBuffers = [];
        }
    }

}

/**
 * @template:
 *      - T: the structure describing the material
 */
pub class UniformBufferObject {record T} over UniformBufferObject {

    /**
     * Create a material and all its buffers
     */
    pub self (dmut shader : &Shader, index : u32)
        with super (alias shader, T::size)
        throws BalderError
    {
        let dmut device = shader:.getDevice ();
        for _ in 0 .. device.getSwapchainNbFrames () {
            let dmut buffer = device:.getMemoryAllocator ():.allocBuffer (
                T::size,
                cast!u32 (VkBufferUsageFlagBits::VK_BUFFER_USAGE_UNIFORM_BUFFER_BIT),
                cast!u32 (VkMemoryPropertyFlagBits::VK_MEMORY_PROPERTY_HOST_VISIBLE_BIT |
                          VkMemoryPropertyFlagBits::VK_MEMORY_PROPERTY_HOST_COHERENT_BIT));

            buffer:.map ();
            self._uniformBuffers ~= [alias buffer];
        }

        self._descSet:.bind (alias self._uniformBuffers, index);
    }

    pub fn update (mut self, data : T) {
        for dmut buf in alias self._uniformBuffers {
            buf:.copyMappedData (&data);
        }
    }


}
