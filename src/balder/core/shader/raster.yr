in raster;

use balder::core::{_,
                   application::_,
                   config::_,
                   shader::_,
                   driver::_};

use balder::utils::_;
use balder::math::vector;

use ::vulkan::core;
use std::{conv, fs::_};

/**
 * A shader used that includes a rasterization pass (vertex, fragment, etc.)
 * In distinction to compute shaders
 */
pub class RasterShader over Shader {

    /**
     * ======================================================================================================
     * ======================================================================================================
     * =======================================   CTOR AND CONFIGURE  ========================================
     * ======================================================================================================
     * ======================================================================================================
     */

    pub self (dmut device : &VulkanDevice)
        with super (alias device)
    {}

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ===================================          CONFIGURE          ====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    /**
     * Configure the shader from a toml file
     * */
    pub fn configure (mut self, file : [c8])
        throws BalderError
    {
        let cfg = copy ShaderConfig (file);
        self:.configure (file, cfg, null, 0u32, 0u32, [])
    }

    /**
     * Configure the shader from a preloaded configuration
     * */
    pub fn configure (mut self, name : [c8], cfg : &ShaderConfig, renderPass : VkRenderPass, w : u32, h : u32, outTextures : [&OutputTexture])
        throws BalderError
    {
        name;
        log::debug #("RasterShader", "Configure shader from", name);
        self:.dispose ();

        self._uniformInfos = (cfg.getUniforms ())?;
        self._textureInfos = (cfg.getTextures ())?;

        self._pipelineLayout:.configure (cfg);

        let dmut stages = copy [VkPipelineShaderStageCreateInfo () ; cfg.getPasses ().len];
        let mut modules : [mut VkShaderModule] = copy [null ; cfg.getPasses ().len];
        {
            for i, p in cfg.getPasses () {
                let stageContent = self.readFile (balder::utils::path::projectPath (p.shaderFile));
                modules [i] = self:.createShaderModule (stageContent);

                let mut info = VkPipelineShaderStageCreateInfo ();
                info.sType = VkStructureType::VK_STRUCTURE_TYPE_PIPELINE_SHADER_STAGE_CREATE_INFO;
                info.stage = p.stage;
                info.module = modules [i];
                info.pName = std::conv::toStringZ (p.mainFunc);
                stages [i] = info;
            }

            if stages.len == 0 {
                throw copy BalderError ("Cannot create shader with no stages");
            }

            let pass = if renderPass != null {
                renderPass
            } else {
                self._device.getRenderPass ()
            };

            let extent = if (w == 0 || h == 0) {
                self._device.getSwapchainExtent ()
            } else {
                let mut ext = VkExtent2D ();
                ext.width = w;
                ext.height = h;
                ext
            };

            self._program = self:.createPipeline (cfg, stages, pass, extent, forSubPass-> (renderPass != null), outTextures-> outTextures);

        } exit {
            for z in modules {
                if z != null {
                    vkDestroyShaderModule (self._device.getDeviceHandle (), z, null);
                }
            }
        }
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * =================================          CONFIGURATION          ==================================
     * ====================================================================================================
     * ====================================================================================================
     */

    /**
     * Create the pipeline of the shader
     */
    fn createPipeline (mut self,
                       cfg : &ShaderConfig,
                       stages : [VkPipelineShaderStageCreateInfo],
                       renderPass : VkRenderPass,
                       extent : VkExtent2D,
                       forSubPass : bool,
                       outTextures : [&OutputTexture])-> dmut VkPipeline
        throws BalderError
    {
        let (attrDescr, bindDescr) = self.createAttributeDescription (cfg);

        let mut vertexInputInfo = VkPipelineVertexInputStateCreateInfo ();
        vertexInputInfo.sType                           = VkStructureType::VK_STRUCTURE_TYPE_PIPELINE_VERTEX_INPUT_STATE_CREATE_INFO;
        vertexInputInfo.vertexBindingDescriptionCount   = 1u32;
        vertexInputInfo.pVertexBindingDescriptions      = &bindDescr;
        vertexInputInfo.vertexAttributeDescriptionCount = cast!u32 (attrDescr.len);
        vertexInputInfo.pVertexAttributeDescriptions    = attrDescr.ptr;

        let mut inputAssembly = VkPipelineInputAssemblyStateCreateInfo ();
        inputAssembly.sType                  = VkStructureType::VK_STRUCTURE_TYPE_PIPELINE_INPUT_ASSEMBLY_STATE_CREATE_INFO;
        inputAssembly.topology               = VkPrimitiveTopology::VK_PRIMITIVE_TOPOLOGY_TRIANGLE_LIST;
        inputAssembly.primitiveRestartEnable = VK_FALSE;

        let mut viewport = VkViewport ();
        viewport.x        = 0.0f;
        viewport.y        = 0.0f;
        viewport.width    = cast!f32 (extent.width);
        viewport.height   = cast!f32 (extent.height);
        viewport.minDepth = 0.0f;
        viewport.maxDepth = 1.0f;

        let mut scissors = VkRect2D ();
        scissors.extent = extent;

        let dynStateList = copy [VkDynamicState::VK_DYNAMIC_STATE_VIEWPORT,
                                 VkDynamicState::VK_DYNAMIC_STATE_SCISSOR];

        let mut dynState = VkPipelineDynamicStateCreateInfo ();
        dynState.sType              = VkStructureType::VK_STRUCTURE_TYPE_PIPELINE_DYNAMIC_STATE_CREATE_INFO;
        dynState.dynamicStateCount  = cast!u32 (dynStateList.len);
        dynState.pDynamicStates     = dynStateList.ptr;

        let mut viewportState = VkPipelineViewportStateCreateInfo ();
        viewportState.sType         = VkStructureType::VK_STRUCTURE_TYPE_PIPELINE_VIEWPORT_STATE_CREATE_INFO;
        viewportState.viewportCount = 1u32;
        viewportState.pViewports    = &viewport;
        viewportState.scissorCount  = 1u32;
        viewportState.pScissors     = &scissors;

        let mut rasterizer = VkPipelineRasterizationStateCreateInfo ();
        rasterizer.sType                   = VkStructureType::VK_STRUCTURE_TYPE_PIPELINE_RASTERIZATION_STATE_CREATE_INFO;
        rasterizer.depthClampEnable        = VK_FALSE;
        rasterizer.rasterizerDiscardEnable = VK_FALSE;
        rasterizer.polygonMode             = VkPolygonMode::VK_POLYGON_MODE_FILL;
        rasterizer.cullMode                = cast!u32 (VkCullMofnlagBits::VK_CULL_MODE_BACK_BIT);
        rasterizer.frontFace               = VkFrontFace::VK_FRONT_FACE_CLOCKWISE;
        rasterizer.depthBiasEnable         = VK_FALSE;
        rasterizer.lineWidth               = 1.0f;

        let mut multisampling = VkPipelineMultisampleStateCreateInfo ();
        multisampling.sType                 = VkStructureType::VK_STRUCTURE_TYPE_PIPELINE_MULTISAMPLE_STATE_CREATE_INFO;
        multisampling.sampleShadingEnable   = VK_FALSE;
        multisampling.minSampleShading      = 1.0f;
        multisampling.alphaToCoverageEnable = VK_FALSE;
        multisampling.alphaToOneEnable      = VK_FALSE;

        // multisampling only enabled for final swapchain
        if !forSubPass {
            multisampling.rasterizationSamples  = self._device.getSwapchainNumSamples ();
        } else {
            multisampling.rasterizationSamples  = VkSampleCountFlagBits::VK_SAMPLE_COUNT_1_BIT;
        }

        let mut depthStencil = VkPipelineDepthStencilStateCreateInfo ();
        depthStencil.sType = VkStructureType::VK_STRUCTURE_TYPE_PIPELINE_DEPTH_STENCIL_STATE_CREATE_INFO;
        if cfg.depth {
            depthStencil.depthTestEnable = VK_TRUE;
            depthStencil.depthWriteEnable = VK_TRUE;
            depthStencil.depthCompareOp = VkCompareOp::VK_COMPARE_OP_LESS;
            depthStencil.depthBoundsTestEnable = VK_FALSE;
            depthStencil.stencilTestEnable = VK_FALSE;
        } else {
            depthStencil.depthTestEnable = VK_FALSE;
        }


        let color = if outTextures.len == 0 {
            self.createDefaultDepthColorAttachment (cfg)
        } else {
            self.createDeferredDepthColorAttachment (cfg, outTextures)
        };

        let mut pipelineInfo = VkGraphicsPipelineCreateInfo ();
        pipelineInfo.sType               = VkStructureType::VK_STRUCTURE_TYPE_GRAPHICS_PIPELINE_CREATE_INFO;
        pipelineInfo.stageCount          = cast!u32 (stages.len);
        pipelineInfo.pStages             = stages.ptr;
        pipelineInfo.pVertexInputState   = &vertexInputInfo;
        pipelineInfo.pInputAssemblyState = &inputAssembly;
        pipelineInfo.pViewportState      = &viewportState;
        pipelineInfo.pRasterizationState = &rasterizer;
        pipelineInfo.pMultisampleState   = &multisampling;
        pipelineInfo.pDepthStencilState  = &depthStencil;
        pipelineInfo.pColorBlendState    = &color;
        pipelineInfo.pDynamicState       = &dynState;
        pipelineInfo.renderPass          = renderPass;
        pipelineInfo.layout              = self._pipelineLayout.getVulkanHandle ();
        pipelineInfo.subpass             = 0u32;
        pipelineInfo.basePipelineHandle  = null;
        pipelineInfo.basePipelineIndex   = -1;

        let dmut pipeline : VkPipeline = null;
        if (vkCreateGraphicsPipelines (self._device.getDeviceHandle (), null, 1u32, &pipelineInfo, null, alias &pipeline) != VkResult::VK_SUCCESS) {
            throw copy BalderError ("Failed to create graphic pipeline");
        }

        alias pipeline
    }

    /**
     * Create the vertex description of the input vertex type of the shader
     */
    fn createAttributeDescription (self, cfg : &ShaderConfig)-> ([VkVertexInputAttributeDescription], VkVertexInputBindingDescription) {
        let (infos, stride) = self.generateFieldInfos (cfg);
        let dmut res = copy [VkVertexInputAttributeDescription () ; infos.len];

        for i in 0us .. infos.len {
            let mut attrDesc = VkVertexInputAttributeDescription ();
            attrDesc.binding  = 0;
            attrDesc.location = cast!u32 (i);
            attrDesc.format   = infos [i]._1;
            attrDesc.offset   = infos [i]._0;
            res[i] = attrDesc;
        }


        let mut bind = VkVertexInputBindingDescription ();
        bind.binding   = 0u32;
        bind.stride    = stride;
        bind.inputRate = VkVertexInputRate::VK_VERTEX_INPUT_RATE_VERTEX;

        (res, bind)
    }

    /**
     * Create the default color attachment (supposing we just return one color texture)
     * */
    fn createDefaultDepthColorAttachment (self, cfg : &ShaderConfig)-> VkPipelineColorBlendStateCreateInfo {
        let mut colorBlendAttachment = VkPipelineColorBlendAttachmentState ();
        colorBlendAttachment.colorWriteMask      = cast!u32 (VkColorComponentFlagBits::VK_COLOR_COMPONENT_R_BIT |
                                                              VkColorComponentFlagBits::VK_COLOR_COMPONENT_G_BIT |
                                                              VkColorComponentFlagBits::VK_COLOR_COMPONENT_B_BIT |
                                                              VkColorComponentFlagBits::VK_COLOR_COMPONENT_A_BIT);
        if cfg.hasBlend () {
            colorBlendAttachment.blendEnable         = VK_TRUE;
        } else {
            colorBlendAttachment.blendEnable         = VK_FALSE;
        }

        colorBlendAttachment.srcColorBlendFactor = cfg.blend.srcColorBlendFactor;
        colorBlendAttachment.dstColorBlendFactor = cfg.blend.dstColorBlendFactor;
        colorBlendAttachment.srcAlphaBlendFactor = cfg.blend.srcAlphaBlendFactor;
        colorBlendAttachment.dstAlphaBlendFactor = cfg.blend.dstAlphaBlendFactor;
        colorBlendAttachment.colorBlendOp        = cfg.blend.colorBlendOp;
        colorBlendAttachment.alphaBlendOp        = cfg.blend.alphaBlendOp;

        let dmut colorAttach = copy [colorBlendAttachment];
        let mut colorBlending = VkPipelineColorBlendStateCreateInfo ();
        colorBlending.sType           = VkStructureType::VK_STRUCTURE_TYPE_PIPELINE_COLOR_BLEND_STATE_CREATE_INFO;
        colorBlending.logicOpEnable   = VK_FALSE;
        colorBlending.logicOp         = VkLogicOp::VK_LOGIC_OP_COPY;
        colorBlending.attachmentCount = cast!u32 (colorAttach.len);
        colorBlending.pAttachments    = colorAttach.ptr;

        colorBlending
    }

    /**
     * Create color and depth attachment for deferred shading
     * */
    fn createDeferredDepthColorAttachment (self, cfg : &ShaderConfig, outTextures : [&OutputTexture])-> VkPipelineColorBlendStateCreateInfo {
        let dmut attachment : [VkPipelineColorBlendAttachmentState] = [];

        for t in outTextures if !t.isDepth () {            
            let mut colorBlendAttachment = VkPipelineColorBlendAttachmentState ();
            colorBlendAttachment.colorWriteMask      = cast!u32 (VkColorComponentFlagBits::VK_COLOR_COMPONENT_R_BIT |
                                                                 VkColorComponentFlagBits::VK_COLOR_COMPONENT_G_BIT |
                                                                 VkColorComponentFlagBits::VK_COLOR_COMPONENT_B_BIT |
                                                                 VkColorComponentFlagBits::VK_COLOR_COMPONENT_A_BIT);

            colorBlendAttachment.blendEnable         = VK_FALSE;
            colorBlendAttachment.srcColorBlendFactor = cfg.blend.srcColorBlendFactor;
            colorBlendAttachment.dstColorBlendFactor = cfg.blend.dstColorBlendFactor;
            colorBlendAttachment.srcAlphaBlendFactor = cfg.blend.srcAlphaBlendFactor;
            colorBlendAttachment.dstAlphaBlendFactor = cfg.blend.dstAlphaBlendFactor;
            colorBlendAttachment.colorBlendOp        = cfg.blend.colorBlendOp;
            colorBlendAttachment.alphaBlendOp        = cfg.blend.alphaBlendOp;

            attachment ~= [colorBlendAttachment];
        }        

        let mut colorBlending = VkPipelineColorBlendStateCreateInfo ();
        colorBlending.sType           = VkStructureType::VK_STRUCTURE_TYPE_PIPELINE_COLOR_BLEND_STATE_CREATE_INFO;
        colorBlending.logicOpEnable   = VK_FALSE;
        colorBlending.logicOp         = VkLogicOp::VK_LOGIC_OP_COPY;
        colorBlending.attachmentCount = cast!u32 (attachment.len);
        colorBlending.pAttachments    = attachment.ptr;

        colorBlending
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * =====================================          FIELDS          =====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    fn generateFieldInfos (self, cfg : &ShaderConfig)-> ([(u32, VkFormat)], u32) {
        let dmut result : [(u32, VkFormat)] = [];
        let mut currOffset = 0u32;
        for entry in VertexEntry::__members__ {
            let (format, size) = cfg.vertexEntry (entry);
            if size != 0 {
                result ~= [(currOffset, format)];
                currOffset += size;
            }
        }

        (result, currOffset)
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ===================================          DISPOSING          ====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    impl Disposable {
        pub over dispose (mut self) {
            self._pipelineLayout:.dispose ();
            if (self._program != null) {
                vkDestroyPipeline (self._device.getDeviceHandle (), self._program, null);
                self._program = null;
            }

            self._uniformInfos = none;
            self._textureInfos = none;
        }
    }

}
